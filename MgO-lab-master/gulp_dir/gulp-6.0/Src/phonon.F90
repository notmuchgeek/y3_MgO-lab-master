  subroutine phonon(lprint,fc,nobsmodeptr0,nobsmode)
!
!  Calculates the phonons at a given set of k points
!
!  The block diagonal elements of derv2 must be stored, as these are
!  not recalculated in dynamic as the summing of off diagonals is no
!  longer applicable.
!
!  nkpt    = total number of k points across all structures
!  nllkpt  = number of k points for this structure
!  nlkpt   = pointer to lowest k point
!  nukpt   = pointer to upper k point
!  xkpt    = fractional x component of k point
!  ykpt    = fractional y component of k point
!  zkpt    = fractional z component of k point
!  wkpt    = weight of each k point
!  nkptcfg = configuration pointer for each k point
!  sumwkpt = sum over weights of k points
!  leigloc = local flag to indicate whether eigenvectors are 
!            to be generated for this configuration
!
!   1/97 For C call to malloc eigv must be declared as real*8 -
!        this makes no difference as storage is the same and
!        complex attribute is handled in subroutines
!   3/97 Error corrected in frequencies for partially occupied
!        systems - need to reduce down atoms to proper number
!   3/97 Pointer to second derivative workspace in eigv for
!        peigen fixed.
!   3/97 Number of K points per call of dynamic reduced to 1
!        for simplicity to make free energy gradients easier.
!        With dynamic memory this nearly always happens anyway.
!   3/97 Bug storage needed for outphon fixed
!   3/97 Multiple temperatures for phonon properties added
!  11/97 Memory savings introduced for gamma point case
!        and CCALL declaration of eigv correct to complex*16
!  11/97 dervi only preserved if free energy opt, rather than
!        some general opt
!  11/97 call of outphon and writing of scratch file on channel
!        51 is now suppressed if no PDOS or dispersion curves
!        are to be generated. This also removes the need for
!        nllkpt*mcv amount of memory - when this is needed the
!        amount set used to be in error by a factor of mcv.
!  11/97 option to write out binary or text list of frequencies
!        to a file => used for Gruneisen parameter calculation
!   4/98 Use of scratch file on channel 51 has been removed in
!        favour of dynamic memory as this was causing errors on
!        DEC machines.
!   5/98 New dynamic memory allocation method in which space is
!        allocated in the main call and passed down through
!        derv2/dervi has been added for Linux where the local
!        allocation of memory appears to fail.
!   8/98 Storage of tmat eliminated as phonon should never
!        overwrite this matrix when it is still needed unless it
!        is regenerated by a call to transmat
!   8/98 Need for gamma point call removed by directly adding difference
!        between D(k)-D(0) on the diagonal blocks
!   3/99 Output of force constant matrix option added
!   6/99 Bug in partial occupancy corrected for k.ne.0 case
!   7/01 Print out of weights for K points corrected
!   7/01 Correction to weighting of K points made for multiple temperatures
!   7/01 indfrq2 incrementing corrected
!   2/02 Type of 4th argument to zhidi/zhifa corrected
!   3/02 Checking of lgamma for each K point added back
!   3/02 Non-analytic correction at gamma added
!   4/02 Polycrystalline average of gamma point n-a correction
!        added
!   5/02 K point pointer added for freq for fitting use - frq
!        array removed since no longer needed
!   7/02 Phonon calculation restricted to region 1.
!   7/02 Error in compression of region 1 atoms corrected
!   7/02 Workaround for failure of some machines to properly
!        deallocate memory from the stack added by only
!        allocating the second derivative workspace once
!   8/02 Modifications for output of eigenvectors made
!   9/02 Corrections made to non-analytic correction so that
!        direction of approach is taken from dispersion curve
!        when a K point is part of one
!   9/02 Setting of lgamma flag for K points now uses threshold
!        rather than test against zero
!   2/03 Matrix inversion accelerated through packed storage
!  11/04 Pi accessed from module
!   5/06 Mass now uses species values
!   5/07 Calculation of mean kinetic energy added
!   5/07 Partial occupancy data moved to module
!  12/07 Unused variables removed
!   4/08 Finite difference option added for dynamical matrix at
!        gamma point.
!   6/08 Non-analytical correction suppressed for finite difference
!        calculation.
!   1/09 Call to outfrc removed since it outputs incorrect forces
!   3/09 Setting of diagonal blocks skipped for finite difference case
!   3/09 dynamicn now called if this is a finite difference run
!   4/09 Control of calling outphon modified so that it occurs even if noden keyword is present
!   6/09 Integer formats increased to allow for greater numbers of K points
!   6/09 PDF changes added
!   1/10 Output of equipartition free energy added
!  10/10 Handling of nudpt/nldpt corrected so that dispersion lines are identified
!        for non-analytic correction.
!   2/12 Index for freq in equipartition free energy corrected from k to nk
!   6/12 nobsmodeptr0 and nobsmode added as input
!   6/12 Option to call either eispack or lapack eigensolvers added
!   6/12 Allocation of eigr modified for case when lapack eigensolver is being used
!   6/12 Thermal conductivity calculation added
!   6/12 Calls to phoncopy routines changed to add extra argument
!   7/12 nphonat variables moved to module for general use
!   7/12 nphonat variables now computed in a subroutine setphonptr
!   7/12 fobsmodeover added to store overlap information
!   7/12 Oscillator strengths passed to peigeng for IR intensity calculation
!   8/12 Call to oscillator strength added for non-gamma routine
!   8/12 Call to peigen modified
!  10/12 Variable number of decimal places added for frequency file output
!   1/13 Non-analytic correction turned off for thermal conductivity as this overwrites derv2.
!   7/13 Handling of non-analytical correction for integer k points added
!   9/13 Raman susceptibility for modes added as an option
!   9/13 Calculation of thermodynamics and printing moved to a subroutine vibenergy
!   9/13 ramstrength passed to peigeng
!   9/13 Output of Raman directions added
!  10/13 Call to ramanstrength added
!  12/13 CASTEP phonon output added
!  12/13 leig removed from argument list to peigen(g)
!   2/14 Integer kpoint flag corrected to factors of 2. 
!   4/14 Non-analytic correction excluded for a single k point unless the direction of
!        approach has been input
!   8/14 Call to changemaxfreqat added
!   8/14 k point index passed to phoncopy2 routines / pdiagl
!   8/14 Compression of derv2/dervi now uses compressd2 subroutine
!   9/14 Logic corrected for savederv2 allocation related to sense of lnonanal changing
!   9/14 Saving and adding back of breathing shell diagonal blocks removed since the
!        self terms are computed by realp on each call
!  10/14 Changed so that fractional k point is passed to dynamic rather than k point
!        index
!  10/14 Call added to handle shells in group velocity calculation
!   2/15 Call to outshengBTE moved here from optim
!   4/15 lusefc added to call to outshengBTEcontrol
!   5/15 Analytical calculation of third order force constants added
!   7/15 OutshengBTE work moved to the end of the routine since it
!        no longer relies on the pre-existing state of the second
!        derivatives
!  12/15 thermalconductivity renamed to thermalconductivity_af
!   2/16 Use of nllkpt for write statement corrected to nlkpt
!   7/16 lshengBTE3rd added
!   8/16 IR intensities now stored in global array
!   9/16 Logical around allocation & deallocation of savederv2/savedervi corrected
!   9/16 nphonatptr no longer allocated or deallocated since it is in a module
!  11/16 Modifications for parallel second derivatives started
!  12/16 mcvrptr added to pass to lower
!  12/16 mcvptr added to pass to oscillatorstrengthg
!  12/16 Merge cluster frequency functionality into this routine
!  11/17 leigloc set to true for MSD calc
!  11/17 wk now passed to peigen
!   1/18 Grueneisen parameters added
!   1/18 Set up of fourbody list added for Grueneisen calculation
!   1/18 Trace added
!   3/18 dynam shortened to dyna in keyword check
!   4/18 leigloc now set here for llower and linten
!   3/19 Multiple temperature ramps added
!   5/19 Finite difference flag split for first and second derivatives
!   8/19 Uncompress imaginary dynamical matrix only written if not gamma
!   8/19 Modifications to breathing shell compression in response to 
!        change in storage pattern in realp
!   8/19 Handling of partial occupancy compression corrected
!   9/19 nrel2 / nrelat changed to nrela2f / nrelf2a
!   4/20 Rigid molecule modifications added
!   4/20 maxfqat changed to maxfreq since it is no longer just atom based
!   4/20 Mass arrays now use fmass and rfmass
!   4/20 Use of constant to test whether a number is too small added
!   5/20 Handling of diagonal blocks added for rigid molecules
!   5/20 Calculation of molaxes added
!   5/20 mcvptr/mcvrptr changed to mtvptr/mtvrptr
!   5/20 ncfoc removed from arguments to groupvelocities
!   6/20 Correction to integerKpoint logic
!   6/20 Calls to lower removed except for gamma point
!   6/20 Last argument to outphon changed
!   6/20 Criteria for being gamma point made consistent
!   6/20 nmolcore changes added
!   6/20 Order of operations changed - shells now handled before
!        generating molecular terms so that eckart works with shells
!   7/20 bornk passed as local array
!
!  Conditions of use:
!
!  GULP is available free of charge to academic institutions
!  and non-commerical establishments only. Copies should be
!  obtained from the author only and should not be distributed
!  in any form by the user to a third party without the express
!  permission of the author. This notice applies to all parts
!  of the program, except any library routines which are
!  distributed with the code for completeness. All rights for
!  such routines remain with the original distributor.
!
!  No claim is made that this program is free from errors and
!  no liability will be accepted for any loss or damage that
!  may result. The user is responsible for checking the validity
!  of their results.
!
!  Copyright Curtin University 2020
!
!  Julian Gale, CIC, Curtin University, July 2020
!
  use configurations
  use g_constants
  use control
  use current
  use derivatives
  use dispersion
  use element
  use gulp_files
  use four,            only : nfor
  use frequencies
  use general
  use gulp_cml,        only : lcml
  use gulp_cml_phonon, only : gulp_cml_startPhonons, gulp_cml_outPDF
  use gulp_cml_phonon, only : gulp_cml_addKPoint, gulp_cml_endPhonons, gulp_cml_PDFstats
  use iochannels
  use ksample
  use ksample_scatter
  use m_pdf,           only : closepdfphonon, pdfsetup
  use m_pdfneutron
  use maths,           only : leispack_eigensolve
  use molecule
  use observables,     only : fobsmodefreq, fobsmodeover
  use parallel
  use partial
  use phononatoms,     only : nphonat, nphonatptr
  use phononatoms,     only : nphonatc, nphonatcptr
  use phononatoms,     only : nphonatm, nphonatmptr
  use projectdos
  use properties
  use scatterdata,     only : lscattercall
  use species,         only : massspec, natspec, ntypspec
  use shells
  use times
#ifdef TRACE
  use trace,           only : trace_in, trace_out
#endif
  implicit none
!
!  Passed variables
!
  integer(i4),  intent(in)                          :: nobsmodeptr0  ! Pointer to first observable mode - 1
  integer(i4),  intent(in)                          :: nobsmode      ! Number of observable modes if fitting run
  logical,      intent(in)                          :: lprint        ! If true then output results
  real(dp),     intent(in)                          :: fc            ! Internal energy
!
!  Local variables
!
  character(len=5)                                  :: lab1
  character(len=2)                                  :: fstring1
  character(len=2)                                  :: fstring2
  character(len=12)                                 :: fstring
  complex(dpc), dimension(:),     allocatable       :: ctmp
  integer(i4)                                       :: i
  integer(i4)                                       :: ifail
  integer(i4)                                       :: ii
  integer(i4)                                       :: im
  integer(i4)                                       :: indi
  integer(i4)                                       :: indif
  integer(i4)                                       :: indii
  integer(i4)                                       :: indj
  integer(i4)                                       :: indjj
  integer(i4)                                       :: inert(3)
  integer(i4),  dimension(:),     allocatable       :: ipivot
  integer(i4)                                       :: j
  integer(i4)                                       :: jj
  integer(i4)                                       :: jm
  integer(i4)                                       :: job
  integer(i4)                                       :: k
  integer(i4)                                       :: kk
  integer(i4),  dimension(:),     allocatable       :: kpvt
  integer(i4)                                       :: l
  integer(i4)                                       :: m
  integer(i4)                                       :: mcv
  integer(i4)                                       :: mcnmv
  integer(i4),  dimension(:),     allocatable       :: mcvptr
  integer(i4)                                       :: mint
  integer(i4)                                       :: mis
  integer(i4)                                       :: mjs
  integer(i4)                                       :: mmv
  integer(i4)                                       :: msv
  integer(i4)                                       :: mtsv      ! Number of vibrational modes plus shell coordinates = mtv + msv
  integer(i4)                                       :: mtv       ! Number of vibrational modes = mcv + mvv
  integer(i4),  dimension(:),     allocatable       :: mtvptr
  integer(i4),  dimension(:),     allocatable       :: mtvrptr
  integer(i4)                                       :: nd
  integer(i4)                                       :: nf
  integer(i4)                                       :: nfitmode
  integer(i4)                                       :: nk
  integer(i4)                                       :: nldpt
  integer(i4)                                       :: nlkpt
  integer(i4)                                       :: nllkpt
  integer(i4)                                       :: nobm
  integer(i4)                                       :: np
  integer(i4)                                       :: np_nlkpt
  integer(i4)                                       :: np_procs
  integer(i4)                                       :: nri
  integer(i4)                                       :: nrj
  integer(i4)                                       :: nsi
  integer(i4)                                       :: nt
  integer(i4)                                       :: ntmax
  integer(i4)                                       :: nudpt
  integer(i4)                                       :: nukpt
  integer(i4)                                       :: status
  logical                                           :: lcmlloc
  logical                                           :: lcluster
  logical                                           :: ldendisp
  logical                                           :: leigloc
  logical                                           :: lfound
  logical                                           :: lgamma
  logical                                           :: lgammaonly
  logical                                           :: lintegerKpoint
  logical                                           :: lintegerKpoint_present
  logical                                           :: lnonanal
  logical                                           :: lnoanald2loc
  logical                                           :: lnoanald3loc
  logical                                           :: lpartofdisp
  logical                                           :: lpdfloc
  logical                                           :: lprinloc
  logical                                           :: ltemprop
  real(dp),     dimension(:),     allocatable       :: averfreq
  real(dp)                                          :: bornkloc(3)
  real(dp)                                          :: bornkpt(3)
  real(dp)                                          :: cmfact
  real(dp)                                          :: g_cpu_time
  real(dp)                                          :: det(2)
  real(dp)                                          :: dx
  real(dp)                                          :: dy
  real(dp)                                          :: dz
  real(dp)                                          :: eigcomp
  real(dp),     dimension(:),     allocatable       :: eigr
  real(dp)                                          :: eigreal
  real(dp)                                          :: factor
  real(dp)                                          :: frq
  real(dp)                                          :: fscale
  real(dp)                                          :: mI(3)
  real(dp),     dimension(:),     allocatable       :: meanKEperatom
  real(dp),     dimension(:,:,:), allocatable       :: oscstrength
  real(dp)                                          :: overlap
  real(dp)                                          :: phi
  real(dp)                                          :: phistep
  real(dp)                                          :: projectionfactor
  real(dp)                                          :: qfrac(3)
  real(dp)                                          :: rkt
  real(dp),     dimension(:,:,:), allocatable       :: ramstrength
  real(dp)                                          :: rmassi
  real(dp)                                          :: rnokpt
  real(dp),     dimension(:),     allocatable       :: savefreq
  real(dp),     dimension(:,:),   allocatable       :: savederv2
  real(dp),     dimension(:,:),   allocatable       :: savedervi
  real(dp),     dimension(:,:),   allocatable       :: Sij
  real(dp)                                          :: sumwkpt
  real(dp)                                          :: t1
  real(dp)                                          :: t1i
  real(dp)                                          :: t1t
  real(dp)                                          :: t2
  real(dp)                                          :: t2i
  real(dp)                                          :: t2t
  real(dp)                                          :: theta
  real(dp)                                          :: thetastep
  real(dp)                                          :: trmke
  real(dp)                                          :: w1
  real(dp)                                          :: w2
  real(dp)                                          :: w3
  real(dp),     dimension(:),     allocatable       :: wtmp
  real(dp)                                          :: weightpt
  real(dp)                                          :: wi
  real(dp)                                          :: wk
  real(dp)                                          :: wr
  real(dp)                                          :: wrk(9)
  real(dp)                                          :: xkt
  real(dp)                                          :: ykt
  real(dp)                                          :: zkt
  real(dp)                                          :: xmod
  real(dp)                                          :: ymod
  real(dp)                                          :: zmod
#ifdef TRACE
  call trace_in('phonon')
#endif
!
  t1t = g_cpu_time()
!
!  Set logicals
!
  leigloc = (leigen.or.leig.or.lcas)
  if ((nprojcfg(ncf)-nprojdef(ncf)).gt.0) leigloc = .true.
  if (.not.lprint) leigloc = .false.
  if (lomega(ncf)) leigloc = .true.
  if (lraman) leigloc = .true.
  if (nbornstep(ncf).gt.0) leigloc = .true.
  if (lmeanke) leigloc = .true.
  if (nobsmode.gt.0) leigloc = .true.
  if (lmsd) leigloc = .true.
  if (lgrueneisen) leigloc = .true.
  if (lthermal) leigloc = .true.
  if (lgroupvelocity) leigloc = .true.
  if (linten) leigloc = .true.
  if (llower) leigloc = .true.
  lcluster = (ndim.eq.0)
  lpdfloc = (lpdf.and..not.lcluster)
  lprinloc = (lprint.and.ioproc)
  ltemprop = (temperature.gt.1.0d-6.or.ntemperatureramp.gt.0)
  lnonanal = (index(keyword,'nono').eq.0.and.ndim.eq.3.and..not.leem.and..not.lnoanald2)
  lnoanald2loc = lnoanald2
  lnoanald3loc = lnoanald3
!
!  Is keyword present to force numerical third order force constants?
!
  if (index(keyword,'num3').eq.1.or.index(keyword,' num3').ne.0) lnoanald3loc = .true.
!
!  For a thermal conductivity calculation we need to turn off the non-analytic correction
!  to avoid the force constant matrix being overwritten.
!
  if (lthermal) lnonanal = .false.
!
!  CML phonon output
!
  lcmlloc = (lcml.and.lfreqout)
  if (lcmlloc) call gulp_cml_startPhonons
!
!  Set constants
!
!  fscale is the conversion factor from (eV/Ang^2)(mol/g) in the dynamical matrix to
!  wavenumbers for the frequencies. The factor of 10^23 comes from Ang^2 -> m^2 and
!  g -> kg.
!
  fscale = sqrt(1.0d23*evtoj*avogadro)
  fscale = fscale/(2.0_dp*pi*speedl)
!
!  Set flag as to whether PDOS or dispersion curves
!  are to be produced
!
  if ((lphono.or.ndline.gt.0.or.index(keyword,'node').eq.0)) then
    ldendisp = lprint
  else
    ldendisp = .false.
  endif
!
!  Work out number of k points for this structure.
!
!  Requires k points to be sorted such that all points
!  relating a given structure are consecutive.
!  
  nlkpt = 0
  do i = 1,nkpt
    nk = nkptcfg(i)
    if (nlkpt.eq.0.and.nk.eq.ncf) nlkpt = i
    if (nk.eq.ncf) nukpt = i
  enddo
!
!  No k points found for current structure, so return
!
  if (nlkpt.eq.0) then
#ifdef TRACE
  call trace_out('phonon')
#endif
    return
  endif
  nllkpt = nukpt - nlkpt + 1
!     
!  Work out lower and upper dispersion lines for this configuration
!     
  nldpt = 0
  if (ndline.gt.0) then
    do i = 1,ndline 
      nd = ndispcfg(i)
      if (nldpt.eq.0.and.nd.eq.ncf) nldpt = i
      if (nd.eq.ncf) nudpt = i
    enddo
  else
    nudpt = -1
  endif
!
!  If gamma approach direction hasn't been input and there is no dispersion then turn off non-analytic correction
!
  if ((nldpt-nudpt).eq.1.and..not.lbornkin(ncf)) lnonanal = .false.
!************************************
!  Build potential lists if needed  *
!************************************
  if (lgrueneisen) then
    if (nfor.gt.0) call setlist4
  endif
!************************************************
!  Find number of atoms for phonon calculation  *
!************************************************
!
!  Set phonon pointers 
!
  call setphonptr
!
!  Allocate local pointer arrays
!
  allocate(meanKEperatom(numat),stat=status)
  if (status/=0) call outofmemory('phonon','meanKEperatom')
  lpocc = (nsfoc+ncfoc.ne.nphonat)
!
!  Calculate a few constants to do with the size of the problem
!
  mint = 3*nphonat
  if (lrigid) then
!
!  Rigid molecules - no partial occupancy
!
    msv = 3*nshell + nbsmatnomol
    mcv = 3*ncore
    mcnmv = 3*ncorenomol
    mmv = 6*nmol
  else
!
!  General case
!
    msv = 3*nsfoc + nbfoc
    mcv = 3*ncfoc
    mcnmv = 3*ncfoc
    mmv = 0
  endif
  mtv = mcnmv + mmv
  mtsv = mtv + msv
!
  if (leckart) then
!
!  Set up pointer to pass to eckart
!
    allocate(mcvptr(mcv),stat=status)
    if (status/=0) call outofmemory('phonon','mcvptr')
    do m = 1,mcv
      mcvptr(m) = m
    enddo
  endif
!
  allocate(mtvptr(mtsv),stat=status)
  if (status/=0) call outofmemory('phonon','mtvptr')
  allocate(mtvrptr(mtsv),stat=status)
  if (status/=0) call outofmemory('phonon','mtvrptr')
!
  do m = 1,mtsv
    mtvptr(m) = m
    mtvrptr(m) = m
  enddo
!
!  Check that maxd2 is greater than or equal to mtv
!
  if (maxd2.lt.mtv) then
    maxd2 = mtv
    call changemaxd2
  endif
!
!  Ensure that frequency array is large enough
!
  call changemaxfreq(mtv)
!
!  Allocate array to hold oscillator strengths if needed
!
  if (leigloc) then
    allocate(savefreq(mtv),stat=status)
    if (status/=0) call outofmemory('phonon','savefreq')
    allocate(oscstrength(3,3,mtv),stat=status)
    if (status/=0) call outofmemory('phonon','oscstrength')
    allocate(ramstrength(3,3,mtv),stat=status)
    if (status/=0) call outofmemory('phonon','ramstrength')
  endif
!
!  Allocate array for thermal conductivity if needed
!
  if (lthermal) then
    allocate(Sij(mtv,mtv),stat=status)
    if (status/=0) call outofmemory('phonon','Sij')
  endif
!
  rkt = boltz*temperature
!
!  If mean KE per atom is requested then initialise array to zero
!
  if (lmeanke) then
    meanKEperatom(1:ncfoc) = 0.0_dp
  endif
!
!  Calculate inversion square root of masses for atoms and momemt of inertia for quaternions
!
!  Now modified to handle partial occupancies
!
  do i = 1,mtv
    fmass(i) = 0.0_dp
  enddo
  do i = 1,nphonatc
    ii = 3*(iocptr(i) - 1)
    nsi = nspecptr(nrelf2a(nphonatcptr(i)))
    rmassi = massspec(nsi)*occuf(nphonatcptr(i))
    if (natspec(nsi).le.maxele.and.abs(rmassi).lt.1.0d-12) then
      call label(natspec(nsi),ntypspec(nsi),lab1)
      call outerror('mass of species '//lab1//' is zero',0_i4)
      goto 999
    endif
    fmass(ii+1) = fmass(ii+1) + rmassi
    fmass(ii+2) = fmass(ii+2) + rmassi
    fmass(ii+3) = fmass(ii+3) + rmassi
  enddo
!
  if (lrigid) then
    do i = 1,nmol
      ii = mcnmv + 6*(i-1)
!
      molaxes(1:3,1:3,i) = 0.0_dp
!
!  Sum atom masses for rigid molecule translations and recompute moment of inertia orientation
!
      do j = 1,nmolcore(i)
        k = nmollist(nmolptr(i)+j)
        nsi = nspecptr(nrelf2a(k))
        rmassi = massspec(nsi)*occuf(k)
        fmass(ii+1) = fmass(ii+1) + rmassi
        fmass(ii+2) = fmass(ii+2) + rmassi
        fmass(ii+3) = fmass(ii+3) + rmassi
!
        dx = molxyz(1,j,i)
        dy = molxyz(2,j,i)
        dz = molxyz(3,j,i)
!
        molaxes(1,1,i) = molaxes(1,1,i) + rmassi*dy*dy + rmassi*dz*dz
        molaxes(2,1,i) = molaxes(2,1,i) - rmassi*dx*dy
        molaxes(3,1,i) = molaxes(3,1,i) - rmassi*dx*dz
        molaxes(1,2,i) = molaxes(1,2,i) - rmassi*dy*dx
        molaxes(2,2,i) = molaxes(2,2,i) + rmassi*dx*dx + rmassi*dz*dz
        molaxes(3,2,i) = molaxes(3,2,i) - rmassi*dy*dz
        molaxes(1,3,i) = molaxes(1,3,i) - rmassi*dz*dx
        molaxes(2,3,i) = molaxes(2,3,i) - rmassi*dz*dy
        molaxes(3,3,i) = molaxes(3,3,i) + rmassi*dx*dx + rmassi*dy*dy
      enddo
!
!  Diagonalise tensor to get axes for moment of inertia tensor
!
      call dsyev('V','U',3_i4,molaxes(1,1,i),3_i4,mI,wrk,9_i4,ifail)
!
!  Moment of inertia for rotations
!
      fmass(ii+4) = mI(1)
      fmass(ii+5) = mI(2)
      fmass(ii+6) = mI(3)
    enddo
  endif
!
  do i = 1,mtv
    if (fmass(i).lt.too_small) then
      if (i.le.mcnmv) then
        call outerror('site has total mass close to zero in phonon',0_i4)
        call stopnow('phonon')
      elseif (mod(i-mcnmv-1,6_i4).lt.3) then
        call outerror('molecule has total mass close to zero in phonon',0_i4)
        call stopnow('phonon')
      else
!
!  Linear molecule - set mass to be large so that frequency is close to zero
!
        fmass(i) = 10000000.0_dp
      endif
    endif
    rfmass(i) = 1.0_dp/sqrt(fmass(i))
  enddo
  if (.not.lnoanald2loc.and..not.lfinitediff2.and..not.lcluster) then
!
!  Store diagonal blocks in derv3 to avoid recalculation
!
    do i = 1,nphonat
!
!  Atom is local to this node
!
      indi = 3*(nphonatptr(i)-1)
      derv3(indi+1,1) = derv2(indi+1,indi+1)
      derv3(indi+2,1) = derv2(indi+2,indi+1)
      derv3(indi+3,1) = derv2(indi+3,indi+1)
      derv3(indi+1,2) = derv2(indi+1,indi+2)
      derv3(indi+2,2) = derv2(indi+2,indi+2)
      derv3(indi+3,2) = derv2(indi+3,indi+2)
      derv3(indi+1,3) = derv2(indi+1,indi+3)
      derv3(indi+2,3) = derv2(indi+2,indi+3)
      derv3(indi+3,3) = derv2(indi+3,indi+3)
      if (nbsmat.gt.0) then
        indi  = mint + i
        indif = 3_i4*numat + nphonatptr(i)
        derv3(indi,1) = derv2(indif,indif)
      endif
    enddo
  endif
!
!  Sum up k point weights and check for gamma point
!
  lgamma = .false.
  lintegerKpoint_present = .false.
  if (lcluster) then
    lgamma = .true.
    lgammaonly = .true.
    sumwkpt = 1.0_dp
  else
    sumwkpt = 0.0_dp
    do i = nlkpt,nukpt
      sumwkpt = sumwkpt + wkpt(i)
      if (abs(xkpt(i)).lt.too_small.and.abs(ykpt(i)).lt.too_small.and.abs(zkpt(i)).lt.too_small) then
        lgamma = .true.
      else
        xmod = mod(xkpt(i)+100.0_dp,1.0_dp)
        ymod = mod(ykpt(i)+100.0_dp,1.0_dp)
        zmod = mod(zkpt(i)+100.0_dp,1.0_dp)
        if (xmod+ymod+zmod.lt.1.0d-10) then
          lintegerKpoint_present = .true.
        endif
      endif
    enddo
!
!  Is the gamma point the only K point?
!
    lgammaonly = (lgamma.and.nllkpt.eq.1)
  endif
!
!  If this is a finite difference second derivative run and there is a non-gamma k-point then stop.
!
  if ((lfinitediff2.or.lnoanald2loc).and..not.lgammaonly) then
    call outerror('Finite difference phonons only allowed for gamma point',0_i4)
    call stopnow('phonon')
  endif
!
  rnokpt = 1.0_dp/sumwkpt
!
!  Ensure we have k vectors
!
  if (ndim.eq.3) then
    call kvector3D
  elseif (ndim.eq.2) then
    call kvector2D
  elseif (ndim.eq.1) then
    call kvector1D
  endif
!
!  Output phonon header
!
  if (lprinloc) then
    if (lcluster) then
      write(ioout,'(/,''  Vibrational Frequency Calculation : '',/)')
    else
      write(ioout,'(/,''  Phonon Calculation : '',/)')
      if ((lgamma.or.lintegerKpoint_present).and.lnonanal) then
        if (nbornstep(ncf).gt.0) then
          write(ioout,'(''  Number of angular steps for n-a correction at gamma = '',i6,/)') nbornstep(ncf)
        else
          lpartofdisp = (nudpt.ge.nldpt)
          if (lpartofdisp) then
            write(ioout,'(''  K direction for n-a correction at gamma ='',3(1x,f8.5))') bornk(1,ncf),bornk(2,ncf),bornk(3,ncf)
            write(ioout,'(''              or the direction of phonon dispersion'',/)')
          else
            write(ioout,'(''  K direction for n-a correction at gamma ='',3(1x,f8.5),/)') bornk(1,ncf),bornk(2,ncf),bornk(3,ncf)
          endif
        endif
      endif
    endif
    if ((lgamma.or.lintegerKpoint_present).and.lraman) then
      if (ramandirtype(ncf).eq.1) then
        write(ioout,'(''  Directions for Raman intensities: Type  =  Cartesian'')')
      else
        write(ioout,'(''  Directions for Raman intensities: Type  =  Fractional '')')
      endif
      write(ioout,'(''                                    In    ='',3(1x,f8.5))') ramandir(1,ncf),ramandir(2,ncf),ramandir(3,ncf)
      write(ioout,'(''                                    Out   ='',3(1x,f8.5),/)') ramandir(4,ncf),ramandir(5,ncf),ramandir(6,ncf)
    endif
    if (lcluster) then
      write(ioout,'(''--------------------------------------------------------------------------------'')')
    else
      write(ioout,'(''  Number of k points for this configuration = '',i8,/)')(nukpt-nlkpt+1)
      write(ioout,'(''--------------------------------------------------------------------------------'')')
    endif
  endif
!**********************************************************************************************
!  If frequencies are to be written to a permanent file then open with the correct type here  *
!**********************************************************************************************
  if (lfrq.and.ioproc) then
    if (index(freqfile,' ').ne.1) then
      if (lfrqbin) then
        open(51,file=freqfile,form='unformatted',status='unknown')
      else
        open(52,file=freqfile,status='unknown')
!
!  Set format statement
!
        fstring = ' '
        call itow(fstring1,nfreqdecimals+5_i4,2_i4)
        call itow(fstring2,nfreqdecimals,2_i4)
        fstring = adjustl(fstring1)//"."//adjustl(fstring2)
      endif
    else
      if (lfrqbin) then
        open(51,form='unformatted',status='unknown')
      else
        open(52,status='unknown')
!
!  Set format statement
!
        fstring = ' '
        call itow(fstring1,nfreqdecimals+5_i4,2_i4)
        call itow(fstring2,nfreqdecimals,2_i4)
        fstring = adjustl(fstring1)//"."//adjustl(fstring2)
      endif
    endif
  endif
!*******************************************************
!  Open eigenvector file if required and write header  *
!*******************************************************
  if (leig.and.ioproc) then
    open(53,file=eigfile,status='unknown',form='formatted')
    write(53,'(i6)') nphonatc
    do i = 1,nphonatc
      ii = nphonatptr(i)
      write(53,'(i3,1x,3(f15.6,1x))') nat(ii),xclat(ii),yclat(ii),zclat(ii)
    enddo
    write(53,'(i6)') nllkpt
    write(53,'(i6)') 3*nphonatc
  endif
!*********************************************************
!  Open CASTEP phonon file if required and write header  *
!*********************************************************
  if (lcas.and.ioproc) then
!
!  Open file
!
    open(54,file=casfile,status='unknown',form='formatted')
!
!  Open file header
!
    write(54,'(a13)') " BEGIN header"
    write(54,'(a22,i6)') " Number of ions       ", nphonatc
    write(54,'(a22,i6)') " Number of branches   ", 3*ncfoc
    write(54,'(a22,i6)') " Number of wavevectors", nllkpt
    write(54,'(a28)') " Frequencies in         cm-1"
!
!  Are intensity units correct?
!
    write(54,'(a36)') " IR intensities in      (D/A)**2/amu" ! ??
    write(54,'(a28)') " Raman intensities in   A**4"         ! ??
!
!  Write out cell vectors
!
    write(54,'(a)')  " Unit cell vectors (A)"
    do i = 1,3
      write(54,'(3f12.6)') (rv(j,i),j=1,3)
    enddo
!
!  Write out a list of fully occupied atoms in the FULL unit cell
!
    write(54,'(a)') " Fractional Co-ordinates"
    do i = 1,nphonatc
      ii = nphonatptr(i)
      call label(nat(ii),nftype(ii),lab1)
      write(54,'(i6,1x,3f12.6,3x,a5,1x,f12.6)') i,xfrac(ii),yfrac(ii),zfrac(ii),lab1,fmass(3*(ii-1)+1)
    enddo
!
!  Close file header
!
    write(54,'(a)') " END header"
  endif
!************************************************
!  Allocate second derivative workspace memory  *
!************************************************
!
!  Serial second derivatives -> Lapack or Eispack
!
  if (leigloc) then
    if (lgammaonly.and.nbornstep(ncf).eq.0) then
      allocate(eigr(maxd2*maxd2),stat=status)
    else
      allocate(eigr(2*maxd2*maxd2),stat=status)
    endif
    if (status/=0) call outofmemory('phonon','eigr')
    if (nbornstep(ncf).gt.0.or.lnonanal) then
      allocate(savederv2(mtv,mtv),stat=status)
      if (status/=0) call outofmemory('phonon','savederv2')
      if (.not.lgammaonly) then
        allocate(savedervi(mtv,mtv),stat=status)
        if (status/=0) call outofmemory('phonon','savedervi')
      endif
    endif
  else
    if (leispack_eigensolve) then
      if (msv.gt.0) then
        if (lgammaonly) then
          allocate(eigr(msv*msv),stat=status)
        else
          allocate(eigr(2*msv*msv),stat=status)
        endif
      else
        allocate(eigr(1),stat=status)
      endif
    elseif (.not.lgammaonly) then
      allocate(eigr(2*maxd2*mtv),stat=status)
    elseif (msv.gt.0) then
      allocate(eigr(msv*(msv+1)/2),stat=status)
    else
      allocate(eigr(1),stat=status)
    endif
    if (status/=0) call outofmemory('phonon','eigr')
  endif
!***********************
!  Setup PDF Variables *
!***********************
  if (lpdfloc) then
    call setuppdfphonon(nllkpt,mcv,nphonatc,nphonatptr,ncf)
  endif
!******************************
!  Initialise MSDs if needed  *
!******************************
  if (lmsd) then
    if (lrigid) then
      msdx(1:ncorenomol+nmol) = 0.0_dp
      msdy(1:ncorenomol+nmol) = 0.0_dp
      msdz(1:ncorenomol+nmol) = 0.0_dp
    else
      msdx(1:ncfoc) = 0.0_dp
      msdy(1:ncfoc) = 0.0_dp
      msdz(1:ncfoc) = 0.0_dp
    endif
  endif
!***********************
!  Loop over k points  *
!***********************
  np_nlkpt = nlkpt
  np_procs = 1_i4
  do k = np_nlkpt,nukpt,np_procs
    if (lpdfloc) then
!
!  Store k point information in PDF module
!
      call setcurk(k,lallk=.true.)
    endif
    if (lscattercall) then
      xkt = xskpt(k)
      ykt = yskpt(k)
      zkt = zskpt(k)
      wk  = wskpt(k)*rnokpt
    else
      xkt = xkpt(k)
      ykt = ykpt(k)
      zkt = zkpt(k)
      wk  = wkpt(k)*rnokpt
    endif
    lgamma = (abs(xkt).lt.too_small.and.abs(ykt).lt.too_small.and.abs(zkt).lt.too_small)
    if (.not.lgamma) then
      xmod = mod(xkt+100.0_dp,1.0_dp)
      ymod = mod(ykt+100.0_dp,1.0_dp)
      zmod = mod(zkt+100.0_dp,1.0_dp)
      lintegerKpoint = (xmod+ymod+zmod.lt.1.0d-10)
    else
      lintegerKpoint = .false.
    endif
!
    if (lfreqout.and.lprinloc.and..not.lcluster) then
      write(ioout,'(''  K point '',i6,'' = '',3f10.6,''  Weight = '',f8.3)') k,xkt,ykt,zkt,wk
      write(ioout,'(''--------------------------------------------------------------------------------'')')
    endif
!
!  Write K point headers to files if required
!
    if (leig.and.ioproc.and..not.lcluster) then
      write(53,'(''K point at '',3f10.6,'' in BZ '')') xkt,ykt,zkt
    endif
    if (lcas.and.ioproc.and..not.lcluster) then
      write(54,'(a10,i5,3f12.6,f14.6)') "     q-pt=",k,xkt,ykt,zkt,wk
    endif
    if (lcluster) then
!
!  Generate derivatives for cluster case by finite differences if not available analytically
!
      if (lnoanald2loc.or.lfinitediff2) then
        call dynamicn
      endif
    else
!
!  Generate phased second derivatives
!
      if (lnoanald2loc.or.lfinitediff2) then
        call dynamicn
      else
        call dynamic(xkt,ykt,zkt)
      endif
    endif
!
    if (.not.lnoanald2loc.and..not.lfinitediff2.and..not.lcluster) then
!
!  Include diagonal blocks, stored in derv3
!
      do i = 1,nphonat
        indi = 3*(nphonatptr(i) - 1)
        derv2(indi+1,indi+1) = derv2(indi+1,indi+1) + derv3(indi+1,1)
        derv2(indi+2,indi+1) = derv2(indi+2,indi+1) + derv3(indi+2,1)
        derv2(indi+3,indi+1) = derv2(indi+3,indi+1) + derv3(indi+3,1)
        derv2(indi+1,indi+2) = derv2(indi+1,indi+2) + derv3(indi+1,2)
        derv2(indi+2,indi+2) = derv2(indi+2,indi+2) + derv3(indi+2,2)
        derv2(indi+3,indi+2) = derv2(indi+3,indi+2) + derv3(indi+3,2)
        derv2(indi+1,indi+3) = derv2(indi+1,indi+3) + derv3(indi+1,3)
        derv2(indi+2,indi+3) = derv2(indi+2,indi+3) + derv3(indi+2,3)
        derv2(indi+3,indi+3) = derv2(indi+3,indi+3) + derv3(indi+3,3)
        if (nbsmat.gt.0) then
          indi = mint + i
          indif = 3_i4*numat + nphonatptr(i)
          derv2(indif,indif) = derv2(indif,indif) + derv3(indi,1)
        endif
      enddo
    endif
!*****************************************************************
!  Compress second derivatives according to partial occupancies  *
!*****************************************************************
    if (lpocc) then
      ncsfoc = ncfoc + nsfoc
      call compressd2(derv2,maxd2,ncfoc,nsfoc,nbfoc,numat,iocptr,ibocptr)
      if (.not.lgamma) then
        call compressd2(dervi,maxd2,ncfoc,nsfoc,nbfoc,numat,iocptr,ibocptr)
      endif
    endif
!----------------------------------------------------------------------------------------------------
!  Construct second derivative matrix : Atom only case                                              |
!----------------------------------------------------------------------------------------------------
    if (numat.ne.nphonat) then
!***************************************************
!  Compress second derivative matrix w.r.t. atoms  *
!***************************************************
      do i = 1,nphonat
        indi  = 3*(i-1)
        indii = 3*(nphonatptr(i)-1)
        do j = 1,nphonat
          indj  = 3*(j-1)
          indjj = 3*(nphonatptr(j)-1)
          derv2(indj+1,indi+1) = derv2(indjj+1,indii+1)
          derv2(indj+2,indi+1) = derv2(indjj+2,indii+1)
          derv2(indj+3,indi+1) = derv2(indjj+3,indii+1)
          derv2(indj+1,indi+2) = derv2(indjj+1,indii+2)
          derv2(indj+2,indi+2) = derv2(indjj+2,indii+2)
          derv2(indj+3,indi+2) = derv2(indjj+3,indii+2)
          derv2(indj+1,indi+3) = derv2(indjj+1,indii+3)
          derv2(indj+2,indi+3) = derv2(indjj+2,indii+3)
          derv2(indj+3,indi+3) = derv2(indjj+3,indii+3)
          if (.not.lgamma) then
            dervi(indj+1,indi+1) = dervi(indjj+1,indii+1)
            dervi(indj+2,indi+1) = dervi(indjj+2,indii+1)
            dervi(indj+3,indi+1) = dervi(indjj+3,indii+1)
            dervi(indj+1,indi+2) = dervi(indjj+1,indii+2)
            dervi(indj+2,indi+2) = dervi(indjj+2,indii+2)
            dervi(indj+3,indi+2) = dervi(indjj+3,indii+2)
            dervi(indj+1,indi+3) = dervi(indjj+1,indii+3)
            dervi(indj+2,indi+3) = dervi(indjj+2,indii+3)
            dervi(indj+3,indi+3) = dervi(indjj+3,indii+3)
          endif
        enddo
      enddo
    endif
!**************************************************************
!  Compress second derivative matrix w.r.t. breathing shells  *
!**************************************************************
    if (nbsmat.gt.0) then
! DEBUG - handle breathing shells for rigid molecule!
      if (.not.lpocc) then
!
!  Full occupancy
!
        mis = 3_i4*numat
        mjs = 3_i4*numat
        do i = 1,nbs
          nri = nphonatptr(nbsptr(i))
          do j = 1,nphonat
            indj = 3*(j-1)
            indjj = 3*(nphonatptr(j) - 1)
            derv2(indj+1,mint+i) = derv2(indjj+1,mis+nri)
            derv2(indj+2,mint+i) = derv2(indjj+2,mis+nri)
            derv2(indj+3,mint+i) = derv2(indjj+3,mis+nri)
            derv2(mint+i,indj+1) = derv2(mis+nri,indjj+1)
            derv2(mint+i,indj+2) = derv2(mis+nri,indjj+2)
            derv2(mint+i,indj+3) = derv2(mis+nri,indjj+3)
          enddo
          do j = 1,nbs
            nrj = nphonatptr(nbsptr(j))
            derv2(mint+j,mint+i) = derv2(mjs+nrj,mis+nri)
          enddo
          if (.not.lgamma) then
            do j = 1,nphonat
              indj = 3*(j-1)
              indjj = 3*(nphonatptr(j) - 1)
              dervi(indj+1,mint+i) = dervi(indjj+1,mis+nri)
              dervi(indj+2,mint+i) = dervi(indjj+2,mis+nri)
              dervi(indj+3,mint+i) = dervi(indjj+3,mis+nri)
              dervi(mint+i,indj+1) = dervi(mis+nri,indjj+1)
              dervi(mint+i,indj+2) = dervi(mis+nri,indjj+2)
              dervi(mint+i,indj+3) = dervi(mis+nri,indjj+3)
            enddo
            do j = 1,nbs
              nrj = nphonatptr(nbsptr(j))
              dervi(mint+j,mint+i) = dervi(mjs+nrj,mis+nri)
            enddo
          endif
        enddo
      endif
    endif
!
!  Output the uncompressed second derivatives for debugging
!
    if (index(keyword,'dyna').ne.0.and.index(keyword,'debu').ne.0.and.ioproc) then
      write(ioout,'(/,''  Uncompressed Real Dynamical matrix :'',/)')
      do i = 1,mtsv
        write(ioout,'(12f11.6)')(derv2(j,i),j=1,mtv+msv)
      enddo
      if (.not.lgamma) then
        write(ioout,'(/,''  Uncompressed Imag Dynamical matrix :'',/)')
        do i = 1,mtsv
          write(ioout,'(12f11.6)')(dervi(j,i),j=1,mtsv)
        enddo
      endif
    endif
!**********************************
!  Eliminate shell contributions  *
!**********************************
    if (msv.gt.0) then
      if (lgamma) then
!**************************
!  Real Matrix Inversion  *
!**************************
        t1i = g_cpu_time()
        ifail = 0                   
!     
!  Allocate workspace for inversion
!     
        allocate(ipivot(msv),stat=status)
        if (status/=0) call outofmemory('phonon','ipivot')                  
        allocate(wtmp(3*msv),stat=status)
        if (status/=0) call outofmemory('phonon','wtmp')
!
!  Transfer data to packed storage
!    
        kk = 0
        do i = 1,msv
          do j = 1,i
            kk = kk + 1
            eigr(kk) = derv2(mcv+j,mcv+i)
          enddo
        enddo                                    
!         
!  Factorise matrix
!  
        call dsptrf('U',msv,eigr,ipivot,ifail)
        if (ifail.eq.0) then
!
!  Form inverse
!
          call dsptri('U',msv,eigr,ipivot,wtmp,ifail)
!
!  Transfer data back
!
          kk = 0
          do i = 1,msv
            do j = 1,i
              kk = kk + 1
              derv2(mcv+j,mcv+i) = eigr(kk)
              derv2(mcv+i,mcv+j) = eigr(kk)
            enddo
          enddo
        endif
!
!  Free workspace  
!
        deallocate(wtmp,stat=status)
        if (status/=0) call deallocate_error('phonon','wtmp')
        deallocate(ipivot,stat=status)
        if (status/=0) call deallocate_error('phonon','ipivot')  
!
        t2i = g_cpu_time()
        tmati = tmati + t2i - t1i
        if (ifail.ne.0) then
          call outerror('inversion of shell 2nd derivatives failed',0_i4)
          call stopnow('phonon')
        endif
!***********************************************
!  Corrected second derivatives = R - T*S-1*T  *
!***********************************************
!
!  First pass : S-1*T stored in diagonally opposite copy of T
!
        do i = 1,mcv
          do j = 1,msv
            wr = 0.0_dp
            do l = 1,msv
              wr = wr + derv2(j+mcv,l+mcv)*derv2(i,l+mcv)
            enddo
            derv2(mcv+j,i) = wr
          enddo
        enddo
!
!  Second pass : T*(S-1*T)
!
        do i = 1,mcv
          do j = 1,mcv
            wr = 0.0_dp
            do l = 1,msv
              wr = wr - derv2(j,l+mcv)*derv2(mcv+l,i)
            enddo
            derv2(j,i) = derv2(j,i) + wr
          enddo
        enddo
      else
!*****************************
!  Complex Matrix Inversion  *
!*****************************
!
!  Transfer real and imaginary components to complex matrix 
!
        call phoncopy1(derv2,dervi,eigr,maxd2,mcv,mcv,msv,msv)
!
!  Invert complex matrix
!
        job = 1
        t1i = g_cpu_time()
        allocate(kpvt(msv),stat=status)
        if (status/=0) call outofmemory('phonon','kpvt')
        call zhifa(eigr,msv,msv,kpvt,ifail)
        if (ifail.ne.0) then
          call outerror('inversion of shell 2nd derivatives failed',0_i4)
          goto 999
        endif
        allocate(ctmp(msv),stat=status)
        if (status/=0) call outofmemory('phonon','ctmp')
        call zhidi(eigr,msv,msv,kpvt,det,inert,ctmp,job)
        deallocate(ctmp,stat=status)
        if (status/=0) call deallocate_error('phonon','ctmp')
        deallocate(kpvt,stat=status)
        if (status/=0) call deallocate_error('phonon','kpvt')
        t2i = g_cpu_time()
        tmati = tmati + t2i - t1i
!
!  Return inverse complex matrix to separate real and imaginary matrices and resymmetrise
!
        call phoncopy2e(0_i4,derv2,dervi,eigr,maxd2,mcv,mcv,msv,msv)
!***********************************************
!  Corrected second derivatives = R - T*S-1*T  *
!***********************************************
!
!  First pass : S-1*T stored in diagonally opposite copy of T
!
        do i = 1,mcv
          do j = 1,msv
            wr = 0.0_dp
            wi = 0.0_dp
            do l = 1,msv
              wr = wr + derv2(j+mcv,l+mcv)*derv2(i,l+mcv)
              wr = wr + dervi(j+mcv,l+mcv)*dervi(i,l+mcv)
              wi = wi + dervi(j+mcv,l+mcv)*derv2(i,l+mcv)
              wi = wi - derv2(j+mcv,l+mcv)*dervi(i,l+mcv)
            enddo
            derv2(mcv+j,i) = wr
            dervi(mcv+j,i) = wi
          enddo
        enddo
!
!  Second pass : T*(S-1*T) - for imaginary case sign has to be adjusted
!  to allow for the fact that T(conj) and T are of opposite signs
!
        do i = 1,mcv
          do j = 1,mcv
            wr = 0.0_dp
            wi = 0.0_dp
            do l = 1,msv
              wr = wr - derv2(j,l+mcv)*derv2(mcv+l,i)
              wr = wr + dervi(j,l+mcv)*dervi(mcv+l,i)
              wi = wi - derv2(j,l+mcv)*dervi(mcv+l,i)
              wi = wi - dervi(j,l+mcv)*derv2(mcv+l,i)
            enddo
            derv2(j,i) = derv2(j,i) + wr
            dervi(j,i) = dervi(j,i) + wi
          enddo
        enddo
!
!  Correct group velocities for shells
!
        if (lgroupvelocity) then
          call shellgroupvelocities(mcv,msv,derv2,dervi,maxd2)
        endif
      endif
    endif
!****************************
!  End of shell correction  *
!****************************
!
!  Output the uncompressed second derivatives for debugging
!
    if (index(keyword,'dyna').ne.0.and.index(keyword,'debu').ne.0.and.ioproc) then
      write(ioout,'(/,''  Uncompressed Real Dynamical matrix for the cores :'',/)')
      do i = 1,mcv
        write(ioout,'(12f11.6)')(derv2(j,i),j=1,mcv)
      enddo
      if (.not.lgamma) then
        write(ioout,'(/,''  Uncompressed Imag Dynamical matrix for the cores :'',/)')
        do i = 1,mcv
          write(ioout,'(12f11.6)')(dervi(j,i),j=1,mcv)
        enddo
      endif
    endif
!**********************************************
!  Option to write out a .frc file for QMPOT  *
!**********************************************
    if (lfrc.and.ioproc.and.lgamma) call outfrc(fc,.true.,.true.)
!************************
!  Eckart purification  *
!************************
    if (leckart.and.lcluster) then
      call eckart(mcv,mcv,mcvptr,maxd2,derv2,maxd2,dervi)
    endif
!----------------------------------------------------------------------------------------------------
!  Construct second derivative matrix : Rigid molecule case                                         |
!----------------------------------------------------------------------------------------------------
    if (lrigid) then
      call rigidmoleculephon(.not.lgamma)
      if (lgroupvelocity) call rigidmoleculegv
!***************************************************
!  Compress second derivative matrix w.r.t. cores  *
!***************************************************
      do i = 1,nphonatc
        indi  = 3*(i-1)
        indii = 3*(nphonatcptr(i)-1)
        do j = 1,nphonatc
          indj  = 3*(j-1)
          indjj = 3*(nphonatcptr(j)-1)
          derv2(indj+1,indi+1) = derv2(indjj+1,indii+1)
          derv2(indj+2,indi+1) = derv2(indjj+2,indii+1)
          derv2(indj+3,indi+1) = derv2(indjj+3,indii+1)
          derv2(indj+1,indi+2) = derv2(indjj+1,indii+2)
          derv2(indj+2,indi+2) = derv2(indjj+2,indii+2)
          derv2(indj+3,indi+2) = derv2(indjj+3,indii+2)
          derv2(indj+1,indi+3) = derv2(indjj+1,indii+3)
          derv2(indj+2,indi+3) = derv2(indjj+2,indii+3)
          derv2(indj+3,indi+3) = derv2(indjj+3,indii+3)
          if (.not.lgamma) then
            dervi(indj+1,indi+1) = dervi(indjj+1,indii+1)
            dervi(indj+2,indi+1) = dervi(indjj+2,indii+1)
            dervi(indj+3,indi+1) = dervi(indjj+3,indii+1)
            dervi(indj+1,indi+2) = dervi(indjj+1,indii+2)
            dervi(indj+2,indi+2) = dervi(indjj+2,indii+2)
            dervi(indj+3,indi+2) = dervi(indjj+3,indii+2)
            dervi(indj+1,indi+3) = dervi(indjj+1,indii+3)
            dervi(indj+2,indi+3) = dervi(indjj+2,indii+3)
            dervi(indj+3,indi+3) = dervi(indjj+3,indii+3)
!
            if (lgroupvelocity) then
              derv2dk(1:3,indj+1,indi+1) = derv2dk(1:3,indjj+1,indii+1)
              derv2dk(1:3,indj+2,indi+1) = derv2dk(1:3,indjj+2,indii+1)
              derv2dk(1:3,indj+3,indi+1) = derv2dk(1:3,indjj+3,indii+1)
              derv2dk(1:3,indj+1,indi+2) = derv2dk(1:3,indjj+1,indii+2)
              derv2dk(1:3,indj+2,indi+2) = derv2dk(1:3,indjj+2,indii+2)
              derv2dk(1:3,indj+3,indi+2) = derv2dk(1:3,indjj+3,indii+2)
              derv2dk(1:3,indj+1,indi+3) = derv2dk(1:3,indjj+1,indii+3)
              derv2dk(1:3,indj+2,indi+3) = derv2dk(1:3,indjj+2,indii+3)
              derv2dk(1:3,indj+3,indi+3) = derv2dk(1:3,indjj+3,indii+3)
            endif
          endif
        enddo
      enddo
!****************************************
!  Add in rigid molecule contributions  *
!****************************************
!
!  Pure molecule terms
!
      do i = 1,nphonatm
        indi  = mcnmv + 6*(i-1)
        im = nphonatmptr(i)
        do j = 1,nphonatm
          indj  = mcnmv + 6*(j-1)
          jm = nphonatmptr(j)
!
!  Molecule - translation/translation
!
          derv2(indj+1,indi+1) = molTTdrv(1,1,jm,im)
          derv2(indj+2,indi+1) = molTTdrv(2,1,jm,im)
          derv2(indj+3,indi+1) = molTTdrv(3,1,jm,im)
          derv2(indj+1,indi+2) = molTTdrv(1,2,jm,im)
          derv2(indj+2,indi+2) = molTTdrv(2,2,jm,im)
          derv2(indj+3,indi+2) = molTTdrv(3,2,jm,im)
          derv2(indj+1,indi+3) = molTTdrv(1,3,jm,im)
          derv2(indj+2,indi+3) = molTTdrv(2,3,jm,im)
          derv2(indj+3,indi+3) = molTTdrv(3,3,jm,im)
          if (.not.lgamma) then
            dervi(indj+1,indi+1) = molTTdri(1,1,jm,im)
            dervi(indj+2,indi+1) = molTTdri(2,1,jm,im)
            dervi(indj+3,indi+1) = molTTdri(3,1,jm,im)
            dervi(indj+1,indi+2) = molTTdri(1,2,jm,im)
            dervi(indj+2,indi+2) = molTTdri(2,2,jm,im)
            dervi(indj+3,indi+2) = molTTdri(3,2,jm,im)
            dervi(indj+1,indi+3) = molTTdri(1,3,jm,im)
            dervi(indj+2,indi+3) = molTTdri(2,3,jm,im)
            dervi(indj+3,indi+3) = molTTdri(3,3,jm,im)
            if (lgroupvelocity) then
              derv2dk(1:3,indj+1,indi+1) = molTTdk(1:3,1,1,jm,im)
              derv2dk(1:3,indj+2,indi+1) = molTTdk(1:3,2,1,jm,im)
              derv2dk(1:3,indj+3,indi+1) = molTTdk(1:3,3,1,jm,im)
              derv2dk(1:3,indj+1,indi+2) = molTTdk(1:3,1,2,jm,im)
              derv2dk(1:3,indj+2,indi+2) = molTTdk(1:3,2,2,jm,im)
              derv2dk(1:3,indj+3,indi+2) = molTTdk(1:3,3,2,jm,im)
              derv2dk(1:3,indj+1,indi+3) = molTTdk(1:3,1,3,jm,im)
              derv2dk(1:3,indj+2,indi+3) = molTTdk(1:3,2,3,jm,im)
              derv2dk(1:3,indj+3,indi+3) = molTTdk(1:3,3,3,jm,im)
            endif
          endif
!
!  Molecule - quaternion/translation
!
          derv2(indj+4,indi+1) = molQTdrv(1,1,jm,im)
          derv2(indj+5,indi+1) = molQTdrv(2,1,jm,im)
          derv2(indj+6,indi+1) = molQTdrv(3,1,jm,im)
          derv2(indj+4,indi+2) = molQTdrv(1,2,jm,im)
          derv2(indj+5,indi+2) = molQTdrv(2,2,jm,im)
          derv2(indj+6,indi+2) = molQTdrv(3,2,jm,im)
          derv2(indj+4,indi+3) = molQTdrv(1,3,jm,im)
          derv2(indj+5,indi+3) = molQTdrv(2,3,jm,im)
          derv2(indj+6,indi+3) = molQTdrv(3,3,jm,im)
!
          derv2(indj+1,indi+4) = molQTdrv(1,1,im,jm)
          derv2(indj+2,indi+4) = molQTdrv(1,2,im,jm)
          derv2(indj+3,indi+4) = molQTdrv(1,3,im,jm)
          derv2(indj+1,indi+5) = molQTdrv(2,1,im,jm)
          derv2(indj+2,indi+5) = molQTdrv(2,2,im,jm)
          derv2(indj+3,indi+5) = molQTdrv(2,3,im,jm)
          derv2(indj+1,indi+6) = molQTdrv(3,1,im,jm)
          derv2(indj+2,indi+6) = molQTdrv(3,2,im,jm)
          derv2(indj+3,indi+6) = molQTdrv(3,3,im,jm)
!
          if (.not.lgamma) then
            dervi(indj+4,indi+1) = - molQTdri(1,1,jm,im)
            dervi(indj+5,indi+1) = - molQTdri(2,1,jm,im)
            dervi(indj+6,indi+1) = - molQTdri(3,1,jm,im)
            dervi(indj+4,indi+2) = - molQTdri(1,2,jm,im)
            dervi(indj+5,indi+2) = - molQTdri(2,2,jm,im)
            dervi(indj+6,indi+2) = - molQTdri(3,2,jm,im)
            dervi(indj+4,indi+3) = - molQTdri(1,3,jm,im)
            dervi(indj+5,indi+3) = - molQTdri(2,3,jm,im)
            dervi(indj+6,indi+3) = - molQTdri(3,3,jm,im)
!
            dervi(indj+1,indi+4) = molQTdri(1,1,im,jm)
            dervi(indj+2,indi+4) = molQTdri(1,2,im,jm)
            dervi(indj+3,indi+4) = molQTdri(1,3,im,jm)
            dervi(indj+1,indi+5) = molQTdri(2,1,im,jm)
            dervi(indj+2,indi+5) = molQTdri(2,2,im,jm)
            dervi(indj+3,indi+5) = molQTdri(2,3,im,jm)
            dervi(indj+1,indi+6) = molQTdri(3,1,im,jm)
            dervi(indj+2,indi+6) = molQTdri(3,2,im,jm)
            dervi(indj+3,indi+6) = molQTdri(3,3,im,jm)
!
            if (lgroupvelocity) then
              derv2dk(1:3,indj+4,indi+1) = molQTdk(1:3,1,1,jm,im)
              derv2dk(1:3,indj+5,indi+1) = molQTdk(1:3,2,1,jm,im)
              derv2dk(1:3,indj+6,indi+1) = molQTdk(1:3,3,1,jm,im)
              derv2dk(1:3,indj+4,indi+2) = molQTdk(1:3,1,2,jm,im)
              derv2dk(1:3,indj+5,indi+2) = molQTdk(1:3,2,2,jm,im)
              derv2dk(1:3,indj+6,indi+2) = molQTdk(1:3,3,2,jm,im)
              derv2dk(1:3,indj+4,indi+3) = molQTdk(1:3,1,3,jm,im)
              derv2dk(1:3,indj+5,indi+3) = molQTdk(1:3,2,3,jm,im)
              derv2dk(1:3,indj+6,indi+3) = molQTdk(1:3,3,3,jm,im)
!
              derv2dk(1:3,indj+1,indi+4) = conjg(molQTdk(1:3,1,1,im,jm))
              derv2dk(1:3,indj+2,indi+4) = conjg(molQTdk(1:3,1,2,im,jm))
              derv2dk(1:3,indj+3,indi+4) = conjg(molQTdk(1:3,1,3,im,jm))
              derv2dk(1:3,indj+1,indi+5) = conjg(molQTdk(1:3,2,1,im,jm))
              derv2dk(1:3,indj+2,indi+5) = conjg(molQTdk(1:3,2,2,im,jm))
              derv2dk(1:3,indj+3,indi+5) = conjg(molQTdk(1:3,2,3,im,jm))
              derv2dk(1:3,indj+1,indi+6) = conjg(molQTdk(1:3,3,1,im,jm))
              derv2dk(1:3,indj+2,indi+6) = conjg(molQTdk(1:3,3,2,im,jm))
              derv2dk(1:3,indj+3,indi+6) = conjg(molQTdk(1:3,3,3,im,jm))
            endif
          endif
!
!  Molecule - quaternion/quaternion
!
          derv2(indj+4,indi+4) = molQQdrv(1,1,jm,im)
          derv2(indj+5,indi+4) = molQQdrv(2,1,jm,im)
          derv2(indj+6,indi+4) = molQQdrv(3,1,jm,im)
          derv2(indj+4,indi+5) = molQQdrv(1,2,jm,im)
          derv2(indj+5,indi+5) = molQQdrv(2,2,jm,im)
          derv2(indj+6,indi+5) = molQQdrv(3,2,jm,im)
          derv2(indj+4,indi+6) = molQQdrv(1,3,jm,im)
          derv2(indj+5,indi+6) = molQQdrv(2,3,jm,im)
          derv2(indj+6,indi+6) = molQQdrv(3,3,jm,im)
          if (.not.lgamma) then
            dervi(indj+4,indi+4) = molQQdri(1,1,jm,im)
            dervi(indj+5,indi+4) = molQQdri(2,1,jm,im)
            dervi(indj+6,indi+4) = molQQdri(3,1,jm,im)
            dervi(indj+4,indi+5) = molQQdri(1,2,jm,im)
            dervi(indj+5,indi+5) = molQQdri(2,2,jm,im)
            dervi(indj+6,indi+5) = molQQdri(3,2,jm,im)
            dervi(indj+4,indi+6) = molQQdri(1,3,jm,im)
            dervi(indj+5,indi+6) = molQQdri(2,3,jm,im)
            dervi(indj+6,indi+6) = molQQdri(3,3,jm,im)
!
            if (lgroupvelocity) then
              derv2dk(1:3,indj+4,indi+4) = molQQdk(1:3,1,1,jm,im)
              derv2dk(1:3,indj+5,indi+4) = molQQdk(1:3,2,1,jm,im)
              derv2dk(1:3,indj+6,indi+4) = molQQdk(1:3,3,1,jm,im)
              derv2dk(1:3,indj+4,indi+5) = molQQdk(1:3,1,2,jm,im)
              derv2dk(1:3,indj+5,indi+5) = molQQdk(1:3,2,2,jm,im)
              derv2dk(1:3,indj+6,indi+5) = molQQdk(1:3,3,2,jm,im)
              derv2dk(1:3,indj+4,indi+6) = molQQdk(1:3,1,3,jm,im)
              derv2dk(1:3,indj+5,indi+6) = molQQdk(1:3,2,3,jm,im)
              derv2dk(1:3,indj+6,indi+6) = molQQdk(1:3,3,3,jm,im)
            endif
          endif
        enddo
      enddo
!
!  Atom-molecule mixed terms - cores
!
      do i = 1,nphonatm
        indi  = mcnmv + 6*(i-1)
        im = nphonatmptr(i)
        do j = 1,nphonatc
          indj  = 3*(j-1)
          indjj = 3*(nphonatcptr(j)-1)
          derv2(indj+1,indi+1) = molTCdrv(indjj+1,1,im)
          derv2(indj+2,indi+1) = molTCdrv(indjj+2,1,im)
          derv2(indj+3,indi+1) = molTCdrv(indjj+3,1,im)
          derv2(indj+1,indi+2) = molTCdrv(indjj+1,2,im)
          derv2(indj+2,indi+2) = molTCdrv(indjj+2,2,im)
          derv2(indj+3,indi+2) = molTCdrv(indjj+3,2,im)
          derv2(indj+1,indi+3) = molTCdrv(indjj+1,3,im)
          derv2(indj+2,indi+3) = molTCdrv(indjj+2,3,im)
          derv2(indj+3,indi+3) = molTCdrv(indjj+3,3,im)
!
          derv2(indj+1,indi+4) = molQCdrv(indjj+1,1,im)
          derv2(indj+2,indi+4) = molQCdrv(indjj+2,1,im)
          derv2(indj+3,indi+4) = molQCdrv(indjj+3,1,im)
          derv2(indj+1,indi+5) = molQCdrv(indjj+1,2,im)
          derv2(indj+2,indi+5) = molQCdrv(indjj+2,2,im)
          derv2(indj+3,indi+5) = molQCdrv(indjj+3,2,im)
          derv2(indj+1,indi+6) = molQCdrv(indjj+1,3,im)
          derv2(indj+2,indi+6) = molQCdrv(indjj+2,3,im)
          derv2(indj+3,indi+6) = molQCdrv(indjj+3,3,im)
!
          if (.not.lgamma) then
            dervi(indj+1,indi+1) = molTCdri(indjj+1,1,im)
            dervi(indj+2,indi+1) = molTCdri(indjj+2,1,im)
            dervi(indj+3,indi+1) = molTCdri(indjj+3,1,im)
            dervi(indj+1,indi+2) = molTCdri(indjj+1,2,im)
            dervi(indj+2,indi+2) = molTCdri(indjj+2,2,im)
            dervi(indj+3,indi+2) = molTCdri(indjj+3,2,im)
            dervi(indj+1,indi+3) = molTCdri(indjj+1,3,im)
            dervi(indj+2,indi+3) = molTCdri(indjj+2,3,im)
            dervi(indj+3,indi+3) = molTCdri(indjj+3,3,im)
!
            dervi(indj+1,indi+4) = molQCdri(indjj+1,1,im)
            dervi(indj+2,indi+4) = molQCdri(indjj+2,1,im)
            dervi(indj+3,indi+4) = molQCdri(indjj+3,1,im)
            dervi(indj+1,indi+5) = molQCdri(indjj+1,2,im)
            dervi(indj+2,indi+5) = molQCdri(indjj+2,2,im)
            dervi(indj+3,indi+5) = molQCdri(indjj+3,2,im)
            dervi(indj+1,indi+6) = molQCdri(indjj+1,3,im)
            dervi(indj+2,indi+6) = molQCdri(indjj+2,3,im)
            dervi(indj+3,indi+6) = molQCdri(indjj+3,3,im)
!
            if (lgroupvelocity) then
              derv2dk(1:3,indj+1,indi+1) = molTCdk(1:3,indjj+1,1,im)
              derv2dk(1:3,indj+2,indi+1) = molTCdk(1:3,indjj+2,1,im)
              derv2dk(1:3,indj+3,indi+1) = molTCdk(1:3,indjj+3,1,im)
              derv2dk(1:3,indj+1,indi+2) = molTCdk(1:3,indjj+1,2,im)
              derv2dk(1:3,indj+2,indi+2) = molTCdk(1:3,indjj+2,2,im)
              derv2dk(1:3,indj+3,indi+2) = molTCdk(1:3,indjj+3,2,im)
              derv2dk(1:3,indj+1,indi+3) = molTCdk(1:3,indjj+1,3,im)
              derv2dk(1:3,indj+2,indi+3) = molTCdk(1:3,indjj+2,3,im)
              derv2dk(1:3,indj+3,indi+3) = molTCdk(1:3,indjj+3,3,im)
!
              derv2dk(1:3,indj+1,indi+4) = molQCdk(1:3,indjj+1,1,im)
              derv2dk(1:3,indj+2,indi+4) = molQCdk(1:3,indjj+2,1,im)
              derv2dk(1:3,indj+3,indi+4) = molQCdk(1:3,indjj+3,1,im)
              derv2dk(1:3,indj+1,indi+5) = molQCdk(1:3,indjj+1,2,im)
              derv2dk(1:3,indj+2,indi+5) = molQCdk(1:3,indjj+2,2,im)
              derv2dk(1:3,indj+3,indi+5) = molQCdk(1:3,indjj+3,2,im)
              derv2dk(1:3,indj+1,indi+6) = molQCdk(1:3,indjj+1,3,im)
              derv2dk(1:3,indj+2,indi+6) = molQCdk(1:3,indjj+2,3,im)
              derv2dk(1:3,indj+3,indi+6) = molQCdk(1:3,indjj+3,3,im)
            endif
          endif
        enddo
      enddo
!
      do i = 1,nphonatc
        indi  = 3*(i-1)
        indii = 3*(nphonatcptr(i)-1)
        do j = 1,nphonatm
          indj  = mcnmv + 6*(j-1)
          jm = nphonatmptr(j)
          derv2(indj+1,indi+1) = molTCdrv(indii+1,1,jm)
          derv2(indj+2,indi+1) = molTCdrv(indii+1,2,jm)
          derv2(indj+3,indi+1) = molTCdrv(indii+1,3,jm)
          derv2(indj+1,indi+2) = molTCdrv(indii+2,1,jm)
          derv2(indj+2,indi+2) = molTCdrv(indii+2,2,jm)
          derv2(indj+3,indi+2) = molTCdrv(indii+2,3,jm)
          derv2(indj+1,indi+3) = molTCdrv(indii+3,1,jm)
          derv2(indj+2,indi+3) = molTCdrv(indii+3,2,jm)
          derv2(indj+3,indi+3) = molTCdrv(indii+3,3,jm)
!
          derv2(indj+4,indi+1) = molQCdrv(indii+1,1,jm)
          derv2(indj+5,indi+1) = molQCdrv(indii+1,2,jm)
          derv2(indj+6,indi+1) = molQCdrv(indii+1,3,jm)
          derv2(indj+4,indi+2) = molQCdrv(indii+2,1,jm)
          derv2(indj+5,indi+2) = molQCdrv(indii+2,2,jm)
          derv2(indj+6,indi+2) = molQCdrv(indii+2,3,jm)
          derv2(indj+4,indi+3) = molQCdrv(indii+3,1,jm)
          derv2(indj+5,indi+3) = molQCdrv(indii+3,2,jm)
          derv2(indj+6,indi+3) = molQCdrv(indii+3,3,jm)
!
          if (.not.lgamma) then
            dervi(indj+1,indi+1) = - molTCdri(indii+1,1,jm)
            dervi(indj+2,indi+1) = - molTCdri(indii+1,2,jm)
            dervi(indj+3,indi+1) = - molTCdri(indii+1,3,jm)
            dervi(indj+1,indi+2) = - molTCdri(indii+2,1,jm)
            dervi(indj+2,indi+2) = - molTCdri(indii+2,2,jm)
            dervi(indj+3,indi+2) = - molTCdri(indii+2,3,jm)
            dervi(indj+1,indi+3) = - molTCdri(indii+3,1,jm)
            dervi(indj+2,indi+3) = - molTCdri(indii+3,2,jm)
            dervi(indj+3,indi+3) = - molTCdri(indii+3,3,jm)
!
            dervi(indj+4,indi+1) = - molQCdri(indii+1,1,jm)
            dervi(indj+5,indi+1) = - molQCdri(indii+1,2,jm)
            dervi(indj+6,indi+1) = - molQCdri(indii+1,3,jm)
            dervi(indj+4,indi+2) = - molQCdri(indii+2,1,jm)
            dervi(indj+5,indi+2) = - molQCdri(indii+2,2,jm)
            dervi(indj+6,indi+2) = - molQCdri(indii+2,3,jm)
            dervi(indj+4,indi+3) = - molQCdri(indii+3,1,jm)
            dervi(indj+5,indi+3) = - molQCdri(indii+3,2,jm)
            dervi(indj+6,indi+3) = - molQCdri(indii+3,3,jm)
!
            if (lgroupvelocity) then
              derv2dk(1:3,indj+1,indi+1) = conjg(molTCdk(1:3,indii+1,1,jm))
              derv2dk(1:3,indj+2,indi+1) = conjg(molTCdk(1:3,indii+1,2,jm))
              derv2dk(1:3,indj+3,indi+1) = conjg(molTCdk(1:3,indii+1,3,jm))
              derv2dk(1:3,indj+1,indi+2) = conjg(molTCdk(1:3,indii+2,1,jm))
              derv2dk(1:3,indj+2,indi+2) = conjg(molTCdk(1:3,indii+2,2,jm))
              derv2dk(1:3,indj+3,indi+2) = conjg(molTCdk(1:3,indii+2,3,jm))
              derv2dk(1:3,indj+1,indi+3) = conjg(molTCdk(1:3,indii+3,1,jm))
              derv2dk(1:3,indj+2,indi+3) = conjg(molTCdk(1:3,indii+3,2,jm))
              derv2dk(1:3,indj+3,indi+3) = conjg(molTCdk(1:3,indii+3,3,jm))
!
              derv2dk(1:3,indj+4,indi+1) = conjg(molQCdk(1:3,indii+1,1,jm))
              derv2dk(1:3,indj+5,indi+1) = conjg(molQCdk(1:3,indii+1,2,jm))
              derv2dk(1:3,indj+6,indi+1) = conjg(molQCdk(1:3,indii+1,3,jm))
              derv2dk(1:3,indj+4,indi+2) = conjg(molQCdk(1:3,indii+2,1,jm))
              derv2dk(1:3,indj+5,indi+2) = conjg(molQCdk(1:3,indii+2,2,jm))
              derv2dk(1:3,indj+6,indi+2) = conjg(molQCdk(1:3,indii+2,3,jm))
              derv2dk(1:3,indj+4,indi+3) = conjg(molQCdk(1:3,indii+3,1,jm))
              derv2dk(1:3,indj+5,indi+3) = conjg(molQCdk(1:3,indii+3,2,jm))
              derv2dk(1:3,indj+6,indi+3) = conjg(molQCdk(1:3,indii+3,3,jm))
            endif
          endif
        enddo
      enddo
    endif
!****************************************
!  Multiply by mass or inertia factors  *
!****************************************
    if (lgamma) then
      do i = 1,mtv
        rmassi = rfmass(i)
        do j = 1,mtv
          derv2(j,i) = rmassi*rfmass(j)*derv2(j,i)
        enddo
      enddo
    else
      do i = 1,mtv
        rmassi = rfmass(i)
        do j = 1,mtv
          derv2(j,i) = rmassi*rfmass(j)*derv2(j,i)
          dervi(j,i) = rmassi*rfmass(j)*dervi(j,i)
        enddo
      enddo
    endif
!
!  If debugging print out dynamical matrix
!
    if (index(keyword,'dyna').ne.0.and.ioproc) then
      write(ioout,'(/,''  Real Dynamical matrix :'',/)')
      do i = 1,mtv
        write(ioout,'(12f11.6)')(derv2(j,i),j=1,mtv)
      enddo
      if (.not.lgamma) then
        write(ioout,'(/,''  Imaginary Dynamical matrix :'',/)')
        do i = 1,mtv
          write(ioout,'(12f11.6)')(dervi(j,i),j=1,mtv)
        enddo
      endif
    endif
!*********************************
!  Diagonalise dynamical matrix  *
!*********************************
    ifail = 0
    if (leigloc) then
!*********************************
!  Eigenvectors and eigenvalues  *
!*********************************
      if (lgamma) then
!
!  Calculate eigenvalues and eigenvectors of uncorrected dynamical matrix
!
        call pdiagg(mtv,maxd2,derv2,eigr,freq(1,k-nlkpt+1),fscale,.true.,lprint,ifail)
!
!  Calculate the oscillator strengths
!
        call oscillatorstrengthg(mtv,mtv,mtvptr,nphonatc,nphonatptr,ncfoc,iocptr, &
                                 eigr,maxd2,oscstrength)
!
!  Optionally call Raman strengths
!
        if (lraman) then
          call ramanstrength(mtv,mtv,mtvptr,nphonatc,nphonatptr,ncfoc,iocptr, &
                             eigr,maxd2,ramstrength)
        endif
!
!  Compute Grueneisen parameters
!
        if (lgrueneisen) then
          call gruneiseng(k-nlkpt+1_i4,mcv,msv,ncfoc,eigr,derv2,maxd2,iocptr,fscale,lprint)
        endif
!
!  Store uncorrected frequencies
!
        savefreq(1:mtv) = freq(1:mtv,k-nlkpt+1)
        if (nbornstep(ncf).eq.0.and.lnonanal) then
!
!  Non-analytic correction for gamma point
!
          if (lnonanal) then
!
!  Find if this point is part of a dispersion curve and if it is substitute direction of approach
!
            lpartofdisp = (nudpt.ge.nldpt)
            if (lpartofdisp) then
              if (k.ge.ndds(nldpt).and.k.le.ndde(nudpt)) then
                lfound = .false.
                nd = nldpt - 1
                do while (.not.lfound.and.nd.lt.nudpt)
                  nd = nd + 1
                  lfound = (k.ge.ndds(nd).and.k.le.ndde(nd))
                enddo
                bornkloc(1) = xkpt(ndde(nd)) - xkpt(ndds(nd))
                bornkloc(2) = ykpt(ndde(nd)) - ykpt(ndds(nd))
                bornkloc(3) = zkpt(ndde(nd)) - zkpt(ndds(nd))
              else
                lpartofdisp = .false.
              endif
            endif
            if (lpartofdisp) then
              if (lrigid) then
                call nagammarigid(mtvrptr,rfmass,bornkloc,maxd2,derv2)
              else
                call nagamma(ncfoc,nphonatc,iocptr,rfmass,bornkloc,maxd2,derv2)
              endif
            else
              bornkpt(1:3) = bornk(1:3,ncf)
              if (lrigid) then
                call nagammarigid(mtvrptr,rfmass,bornkpt,maxd2,derv2)
              else
                call nagamma(ncfoc,nphonatc,iocptr,rfmass,bornkpt,maxd2,derv2)
              endif
            endif
          endif
!
!  Calculate eigenvalues and eigenvectors of corrected dynamical matrix
!
          call pdiagg(mtv,maxd2,derv2,eigr,freq(1,k-nlkpt+1),fscale,.false.,lprint,ifail)
        elseif (nbornstep(ncf).ne.0) then
!******************************************************
!  Polycrystalline average of gamma point correction  *
!******************************************************
!
!  Allocate extra storage
!
          allocate(averfreq(mtv),stat=status)
          if (status/=0) call outofmemory('phonon','averfreq')
!
!  Store derv2
!
          savederv2(1:mtv,1:mtv) = derv2(1:mtv,1:mtv)
          averfreq(1:mtv) = 0.0_dp
!
!  Set theta and phi step sizes
!
          weightpt = 1.0_dp/dble(nbornstep(ncf))
          phistep = 0.5_dp*pi*weightpt
          thetastep = phistep
!
!  Loop over theta and phi in nbornstep divisions per angle
!
          weightpt = 0.0_dp
          phi = - phistep
          do np = 0,nbornstep(ncf)
            phi = phi + phistep
            theta = - thetastep
            if (np.eq.0) then
              ntmax = 1
            elseif (np.eq.nbornstep(ncf)) then
              ntmax = 2*nbornstep(ncf)
            else
              ntmax = 4*nbornstep(ncf)
            endif
            do nt = 1,ntmax
              theta = theta + thetastep
              weightpt = weightpt + 1.0_dp
!
!  Find current Q direction of approach
!
              qfrac(1) = sin(phi)*cos(theta)
              qfrac(2) = sin(phi)*sin(theta)
              qfrac(3) = cos(phi)
!
!  Restore derv2
!
              derv2(1:mtv,1:mtv) = savederv2(1:mtv,1:mtv)
!
!  Non-analytic correction for gamma point
!
              if (lrigid) then
                call nagammarigid(mtvrptr,rfmass,qfrac,maxd2,derv2)
              else
                call nagamma(ncfoc,nphonatc,iocptr,rfmass,qfrac,maxd2,derv2)
              endif
!
!  Calculate eigenvalues and eigenvectors of corrected dynamical matrix
!
              call pdiagg(mtv,maxd2,derv2,eigr(maxd2*maxd2+1),freq(1,k-nlkpt+1),fscale,.true.,lprint,ifail)
!
!  Find correspondence of modes by projection of eigenvectors
!

!
!  Add frequencies to average for appropriate mode
!
              do nf = 1,mtv
                averfreq(nf) = averfreq(nf) + freq(nf,k-nlkpt+1)
              enddo
!
!  End loops over angles
!
            enddo
          enddo
!
!  Move average frequencies back to main frequency array
!
          freq(1:mtv,k-nlkpt+1) = averfreq(1:mtv)/weightpt
!
          deallocate(averfreq,stat=status)
          if (status/=0) call deallocate_error('phonon','averfreq')
        endif
!
!  Output frequencies / DOS / intensities
!
        call peigeng(mtv,freq(1,k-nlkpt+1),ncore,ncfoc,iocptr,maxd2,eigr,oscstrength, &
                     ramstrength,IRintensity(1,k-nlkpt+1))
        if (nobsmode.gt.0) then
!
!  Look for vibrational mode frequencies projected on to eigenvectors
!
          do nobm = 1,nobsmode
            call getvibmode(nobsmodeptr0+nobm,maxd2,eigr,nfitmode,overlap)
            fobsmodefreq(nobsmodeptr0+nobm) = freq(nfitmode,k-nlkpt+1)
            fobsmodeover(nobsmodeptr0+nobm) = overlap
          enddo
        endif
!
!  Thermal conductivity calculation
!
        if (lthermal) then
          call thermalconductivity_af(mtv,derv2,eigr,Sij,freq(1,k-nlkpt+1),nphonatc,ncfoc,nphonatptr,maxd2,fscale)
        endif
!
!  Lower symmetry to remove imaginary modes if selected
!
        call lower(mtv,mtvrptr,freq(1,k-nlkpt+1),nphonatc,nphonatptr,ncfoc,iocptr,maxd2,eigr)
      else
!*****************************************
!  Complex eigenvalues and eigenvectors  *
!*****************************************
        if (lintegerKpoint) then
!
!  Store derv2
!
          if (nbornstep(ncf).gt.0.or.lnonanal) then
            savederv2(1:mtv,1:mtv) = derv2(1:mtv,1:mtv)
            savedervi(1:mtv,1:mtv) = dervi(1:mtv,1:mtv)
          endif
!
!  Calculate eigenvalues and eigenvectors of dynamical matrix
!
          if (leispack_eigensolve) then
            call pdiage(mtv,maxd2,derv2,dervi,eigr(1),eigr(maxd2*maxd2+1),freq(1,k-nlkpt+1),fscale,.true.,lprint,ifail)
          else
            call pdiagl(k-nlkpt+1_i4,mtv,maxd2,derv2,dervi,eigr(1),eigr(maxd2*maxd2+1),freq(1,k-nlkpt+1),fscale,.true.,lprint,ifail)
          endif
!
!  Store uncorrected frequencies
!
          savefreq(1:mtv) = freq(1:mtv,k-nlkpt+1)
!
!  Restore derv2
!
          if (nbornstep(ncf).gt.0.or.lnonanal) then
            derv2(1:mtv,1:mtv) = savederv2(1:mtv,1:mtv)
            dervi(1:mtv,1:mtv) = savedervi(1:mtv,1:mtv)
          endif
!
          if (nbornstep(ncf).eq.0.and.lnonanal) then
!
!  Non-analytic correction for gamma point
!
            if (lnonanal) then
!
!  Find if this point is part of a dispersion curve and if it is substitute direction of approach
!
              lpartofdisp = (nudpt.ge.nldpt)
              if (lpartofdisp) then
                if (k.ge.ndds(nldpt).and.k.le.ndde(nudpt)) then
                  lfound = .false.
                  nd = nldpt - 1
                  do while (.not.lfound.and.nd.lt.nudpt)
                    nd = nd + 1
                    lfound = (k.ge.ndds(nd).and.k.le.ndde(nd))
                  enddo
                  bornkloc(1) = xkpt(ndde(nd)) - xkpt(ndds(nd))
                  bornkloc(2) = ykpt(ndde(nd)) - ykpt(ndds(nd))
                  bornkloc(3) = zkpt(ndde(nd)) - zkpt(ndds(nd))
                else
                  lpartofdisp = .false.
                endif
              endif
              if (lpartofdisp) then
                if (lrigid) then
                  call nagammacrigid(mtvrptr,rfmass,bornkloc,maxd2,derv2,dervi,xkt,ykt,zkt)
                else
                  call nagammac(ncfoc,nphonatc,nphonatptr,iocptr,rfmass,bornkloc,maxd2,derv2,dervi,xkt,ykt,zkt)
                endif
              else
                bornkpt(1:3) = bornk(1:3,ncf)
                if (lrigid) then
                  call nagammacrigid(mtvrptr,rfmass,bornkpt,maxd2,derv2,dervi,xkt,ykt,zkt)
                else
                  call nagammac(ncfoc,nphonatc,nphonatptr,iocptr,rfmass,bornkpt,maxd2,derv2,dervi,xkt,ykt,zkt)
                endif
              endif
            endif
!
!  Calculate eigenvalues and eigenvectors of corrected dynamical matrix
!
            if (leispack_eigensolve) then
              call pdiage(mtv,maxd2,derv2,dervi,eigr(1),eigr(maxd2*maxd2+1),freq(1,k-nlkpt+1),fscale,.true.,lprint,ifail)
            else
              call pdiagl(k-nlkpt+1_i4,mtv,maxd2,derv2,dervi,eigr(1),eigr(maxd2*maxd2+1),freq(1,k-nlkpt+1),fscale,.true., &
                          lprint,ifail)
            endif
          elseif (nbornstep(ncf).ne.0) then
!******************************************************
!  Polycrystalline average of gamma point correction  *
!******************************************************
!
!  Allocate extra storage
!
            allocate(averfreq(mtv),stat=status)
            if (status/=0) call outofmemory('phonon','averfreq')
!
!  Store derv2
!
            savederv2(1:mtv,1:mtv) = derv2(1:mtv,1:mtv)
            savedervi(1:mtv,1:mtv) = dervi(1:mtv,1:mtv)
            averfreq(1:mtv) = 0.0_dp
!
!  Set theta and phi step sizes
!
            weightpt = 1.0_dp/dble(nbornstep(ncf))
            phistep = 0.5_dp*pi*weightpt
            thetastep = phistep
!
!  Loop over theta and phi in nbornstep divisions per angle
!
            weightpt = 0.0_dp
            phi = - phistep
            do np = 0,nbornstep(ncf)
              phi = phi + phistep
              theta = - thetastep
              if (np.eq.0) then
                ntmax = 1
              elseif (np.eq.nbornstep(ncf)) then
                ntmax = 2*nbornstep(ncf)
              else
                ntmax = 4*nbornstep(ncf)
              endif
              do nt = 1,ntmax
                theta = theta + thetastep
                weightpt = weightpt + 1.0_dp
!
!  Find current Q direction of approach
!
                qfrac(1) = sin(phi)*cos(theta)
                qfrac(2) = sin(phi)*sin(theta)
                qfrac(3) = cos(phi)
!
!  Restore derv2
!
                derv2(1:mtv,1:mtv) = savederv2(1:mtv,1:mtv)
                dervi(1:mtv,1:mtv) = savedervi(1:mtv,1:mtv)
!
!  Non-analytic correction for gamma point
!
                if (lrigid) then
                  call nagammacrigid(mtvrptr,rfmass,qfrac,maxd2,derv2,dervi,xkt,ykt,zkt)
                else
                  call nagammac(ncfoc,nphonatc,nphonatptr,iocptr,rfmass,qfrac,maxd2,derv2,dervi,xkt,ykt,zkt)
                endif
!
!  Calculate eigenvalues and eigenvectors of corrected dynamical matrix
!
                if (leispack_eigensolve) then
                  call pdiage(mtv,maxd2,derv2,dervi,eigr(1),eigr(maxd2*maxd2+1),freq(1,k-nlkpt+1),fscale,.true.,lprint,ifail)
                else
                  call pdiagl(k-nlkpt+1_i4,mtv,maxd2,derv2,dervi,eigr(1),eigr(maxd2*maxd2+1),freq(1,k-nlkpt+1),fscale,.true., &
                              lprint,ifail)
                endif
!
!  Add frequencies to average for appropriate mode
!
                do nf = 1,mtv
                  averfreq(nf) = averfreq(nf) + freq(nf,k-nlkpt+1)
                enddo
!
!  End loops over angles
!
              enddo
            enddo
!
!  Move average frequencies back to main frequency array
!
            freq(1:mtv,k-nlkpt+1) = averfreq(1:mtv)/weightpt
!
            deallocate(averfreq,stat=status)
            if (status/=0) call deallocate_error('phonon','averfreq')
          endif
!
!  Calculate the oscillator strengths
!
          call oscillatorstrength(mtv,nphonatc,nphonatptr,ncfoc,iocptr,eigr(1),eigr(maxd2*maxd2+1), &
                                  maxd2,oscstrength)
!
!  Output frequencies / DOS / intensities
!
          call peigen(mtv,freq(1,k-nlkpt+1),ncore,nphonatc,ncfoc,nsfoc,iocptr,maxd2, &
                      eigr(1),eigr(maxd2*maxd2+1),oscstrength,IRintensity(1,k-nlkpt+1),wk)
!
!  Compute group velocitiies
!
          if (lgroupvelocity) then
            call groupvelocities(k-nlkpt+1_i4,mtv,eigr(1),eigr(maxd2*maxd2+1),maxd2,fscale,lprint)
          endif
!
!  Compute Grueneisen parameters
!
          if (lgrueneisen) then
            call gruneisen(k-nlkpt+1_i4,mcv,msv,ncfoc,eigr(1),eigr(maxd2*maxd2+1),derv2,dervi,maxd2,iocptr,fscale,lprint)
          endif
        else
!
!  Calculate eigenvalues and eigenvectors of dynamical matrix
!
          if (leispack_eigensolve) then
            call pdiage(mtv,maxd2,derv2,dervi,eigr(1),eigr(maxd2*maxd2+1),freq(1,k-nlkpt+1),fscale,.true.,lprint,ifail)
          else
            call pdiagl(k-nlkpt+1_i4,mtv,maxd2,derv2,dervi,eigr(1),eigr(maxd2*maxd2+1),freq(1,k-nlkpt+1),fscale,.true.,lprint,ifail)
          endif
!
!  Calculate the oscillator strengths
!
          call oscillatorstrength(mtv,nphonatc,nphonatptr,ncfoc,iocptr,eigr(1),eigr(maxd2*maxd2+1), &
                                  maxd2,oscstrength)
!
!  Output frequencies / DOS / intensities
!
          call peigen(mtv,freq(1,k-nlkpt+1),ncore,nphonatc,ncfoc,nsfoc,iocptr,maxd2, &
                      eigr(1),eigr(maxd2*maxd2+1),oscstrength,IRintensity(1,k-nlkpt+1),wk)
!
!  Compute group velocitiies
!
          if (lgroupvelocity) then
            call groupvelocities(k-nlkpt+1_i4,mtv,eigr(1),eigr(maxd2*maxd2+1),maxd2,fscale,lprint)
          endif
!
!  Compute Grueneisen parameters
!
          if (lgrueneisen) then
            call gruneisen(k-nlkpt+1_i4,mcv,msv,ncfoc,eigr(1),eigr(maxd2*maxd2+1),derv2,dervi,maxd2,iocptr,fscale,lprint)
          endif
        endif
      endif
    else
!*********************
!  Eigenvalues only  *
!*********************
      if (lgamma.or.lintegerKpoint) then
!
!  Non-analytic correction for gamma point
!
        if (lnonanal) then
!
!  Find if this point is part of a dispersion curve and if it is substitute direction of approach
!
          lpartofdisp = (nudpt.ge.nldpt)
          if (lpartofdisp) then
            if (k.ge.ndds(nldpt).and.k.le.ndde(nudpt)) then
              lfound = .false.
              nd = nldpt - 1
              do while (.not.lfound.and.nd.lt.nudpt)
                nd = nd + 1
                lfound = (k.ge.ndds(nd).and.k.le.ndde(nd))
              enddo
              bornkloc(1) = xkpt(ndde(nd)) - xkpt(ndds(nd))
              bornkloc(2) = ykpt(ndde(nd)) - ykpt(ndds(nd))
              bornkloc(3) = zkpt(ndde(nd)) - zkpt(ndds(nd))
            else
              lpartofdisp = .false. 
            endif
          endif
          if (lintegerKpoint) then
            if (lpartofdisp) then
              if (lrigid) then
                call nagammacrigid(mtvrptr,rfmass,bornkloc,maxd2,derv2,dervi,xkt,ykt,zkt)
              else
                call nagammac(ncfoc,nphonatc,nphonatptr,iocptr,rfmass,bornkloc,maxd2,derv2,dervi,xkt,ykt,zkt)
              endif
            else
              bornkpt(1:3) = bornk(1:3,ncf)
              if (lrigid) then
                call nagammacrigid(mtvrptr,rfmass,bornkpt,maxd2,derv2,dervi,xkt,ykt,zkt)
              else
                call nagammac(ncfoc,nphonatc,nphonatptr,iocptr,rfmass,bornkpt,maxd2,derv2,dervi,xkt,ykt,zkt)
              endif
            endif
          else
            if (lpartofdisp) then
              if (lrigid) then
                call nagammarigid(mtvrptr,rfmass,bornkloc,maxd2,derv2)
              else
                call nagamma(ncfoc,nphonatc,iocptr,rfmass,bornkloc,maxd2,derv2)
              endif
            else
              bornkpt(1:3) = bornk(1:3,ncf)
              if (lrigid) then
                call nagammarigid(mtvrptr,rfmass,bornkpt,maxd2,derv2)
              else
                call nagamma(ncfoc,nphonatc,iocptr,rfmass,bornkpt,maxd2,derv2)
              endif
            endif
          endif
        endif
!
!  Calculate eigenvalues of uncorrected dynamical matrix
!
        if (lintegerKpoint) then
          if (leispack_eigensolve) then
            call pdiage(mtv,maxd2,derv2,dervi,eigr,eigr,freq(1,k-nlkpt+1),fscale,.false.,lprint,ifail)
          else
            call pdiagl(k-nlkpt+1_i4,mtv,maxd2,derv2,dervi,eigr,eigr,freq(1,k-nlkpt+1),fscale,.false.,lprint,ifail)
          endif
        else
          call pdiagg(mtv,maxd2,derv2,eigr,freq(1,k-nlkpt+1),fscale,.false.,lprint,ifail)
        endif
      else
!
!  Calculate eigenvalues of dynamical matrix
!
        if (leispack_eigensolve) then
          call pdiage(mtv,maxd2,derv2,dervi,eigr,eigr,freq(1,k-nlkpt+1),fscale,.false.,lprint,ifail)
        else
          call pdiagl(k-nlkpt+1_i4,mtv,maxd2,derv2,dervi,eigr,eigr,freq(1,k-nlkpt+1),fscale,.false.,lprint,ifail)
        endif
      endif
!
!  Output frequencies
!
      if (lfreqout.and.lprinloc) then
        write(ioout,'(/,''  Frequencies (cm-1) [NB: Negative implies an imaginary mode]:'',/)')
        write(ioout,'(9f8.2)')(freq(i,k-nlkpt+1),i=1,mtv)
        write(ioout,'(/)')
      endif
    endif
!
!  Store frequencies if needed for output options
!
    if (ldendisp.or.(lfrq.and.lfrqbin).or.ltemprop.or.(lprinloc.and.(ntemperatureramp.gt.0))) then
      t1 = g_cpu_time()
      if (lfrq.and.ioproc) then
        if (lfrqbin) then
          do i = 1,mtv
            write(51) freq(i,k-nlkpt+1)
          enddo
        else
          do i = 1,mtv
            write(52,"(f" // adjustl(fstring) // ")") freq(i,k-nlkpt+1)
          enddo
        endif
      endif
      t2 = g_cpu_time()
      tdisk = tdisk + t2 - t1
    elseif (lfrq.and.ioproc) then
      t1 = g_cpu_time()
      do i = 1,mtv
        write(52,'(f12.6)') freq(i,k-nlkpt+1)
      enddo
      t2 = g_cpu_time()
      tdisk = tdisk + t2 - t1
    endif
!***************************************
!  Evaluate phonon related properties  *
!***************************************
    if (lmeanke) then
      if (temperature.gt.1.0d-6) then
!
!  Scale frequencies to hw/kT
!
        cmfact = planck*speedl/rkt
        factor = 0.5_dp*wk*rkt/evtoj
        do i = 1,mtv
          frq = cmfact*freq(i,k-nlkpt+1)
!
!  Store exp(x) in w1, exp(x)-1 in w2 and 1/(exp(x)-1) in w3
!
          if (frq.lt.12.0_dp) then
            w1 = exp(frq)
            w2 = w1 - 1.0_dp
            if (abs(w2).gt.0.0_dp) w3 = 1.0_dp/w2
          else
            w3 = exp(-frq)
          endif
!
!
!  Aportion kinetic energy to atoms based on eigenvector contribution
!
          trmke = factor*frq*(0.5_dp + w3)
          if (lgamma) then
            do j = 1,mtv
              jj = (j-1)/3_i4 + 1
              projectionfactor = eigr(maxd2*(i-1)+j)**2
              meanKEperatom(jj) = meanKEperatom(jj) + trmke*projectionfactor
            enddo
          else
            do j = 1,mtv
              jj = (j-1)/3_i4 + 1
              eigreal = eigr(maxd2*(i-1)+j)
              eigcomp = eigr(maxd2*maxd2+maxd2*(i-1)+j)
              projectionfactor = eigreal**2 + eigcomp**2
              meanKEperatom(jj) = meanKEperatom(jj) + trmke*projectionfactor
            enddo
          endif
        enddo
      endif
    endif
!
!  Output eigenvectors if requested
!
    if (lfreqout.and.lprinloc) then
      write(ioout,'(''--------------------------------------------------------------------------------'')')
    endif
!
!  Frequency-dependent properties
!
    if (lomega(ncf).and.lgamma) then
      call omegaproperty(mtv,savefreq,fscale,oscstrength)
    endif
!***************************
!  End loop over K points  *
!***************************
  enddo
  if (lpdfloc) then
!
!  Set up variables for gulp_cml_PDFstats
!
    call pdfsetup(lcml) 
  endif
!**************************************************
!  Deallocate second derivative workspace memory  *
!**************************************************
  deallocate(eigr,stat=status)
  if (status/=0) call deallocate_error('phonon','eigr')
  if (allocated(savedervi)) then
    deallocate(savedervi,stat=status)
    if (status/=0) call deallocate_error('phonon','savedervi')
  endif
  if (allocated(savederv2)) then
    deallocate(savederv2,stat=status)
    if (status/=0) call deallocate_error('phonon','savederv2')
  endif
!*************************************
!  Output phonon related properties  *
!*************************************
!
!  Compute vibrational energies and output
!
  call vibenergy(mtv,nllkpt,wkpt(nlkpt),freq(1,1),maxfreq,rnokpt,lprint,fc)
!
!  Mean squared displacements
!
  if (lprinloc.and.lmsd) then
    write(ioout,'(/,''  Mean-squared displacements for phonons (Ang**2) : '',/)')
    write(ioout,'(''--------------------------------------------------------------------------------'')')
    write(ioout,'(''   Atom     '',8x,''x'',12x,''y'',12x,''z'')')
    write(ioout,'(''--------------------------------------------------------------------------------'')')
    if (lrigid) then
      do i = 1,ncorenomol
        write(ioout,'(i8,5x,3(f12.6,1x))') i,msdx(i),msdy(i),msdz(i)
      enddo
      write(ioout,'(''--------------------------------------------------------------------------------'')')
      write(ioout,'(''   Molecule '',8x,''x'',12x,''y'',12x,''z'')')
      write(ioout,'(''--------------------------------------------------------------------------------'')')
      do i = 1,nmol
        j = ncorenomol + i
        write(ioout,'(i8,5x,3(f12.6,1x))') i,msdx(j),msdy(j),msdz(j)
      enddo
    else
      do i = 1,ncfoc
        write(ioout,'(i8,5x,3(f12.6,1x))') i,msdx(i),msdy(i),msdz(i)
      enddo
    endif
    write(ioout,'(''--------------------------------------------------------------------------------'')')
    write(ioout,'(/)')
  endif
!
!  Mean kinetic energies
!
  if (lprinloc.and.lmeanke) then
    write(ioout,'(''  Mean kinetic energy per site: (eV)'')')
    write(ioout,'(''--------------------------------------------------------------------------------'')')
    if (lrigid) then
      do i = 1,ncorenomol+nmol
        write(ioout,'(i6,2x,f15.6)') i,meanKEperatom(i)
      enddo
    else
      do i = 1,ncfoc
        write(ioout,'(i6,2x,f15.6)') i,meanKEperatom(i)
      enddo
    endif
    write(ioout,'(''--------------------------------------------------------------------------------'')')
  endif
!
!  If output if required then call outphon
!
  if (ldendisp.and.ioproc) then
    call outphon(nlkpt,nukpt,leigloc,mtv)
    if (lfrq.and.lfrqbin) close(51)
  elseif (lfrq.and.lfrqbin.and.ioproc) then
    close(51)
  endif
!***************
!  Exit point  *
!***************
999 continue
!
!  Close I/O channels
!
  if (leig.and.ioproc) then
    close(53)
  endif
  if (lcas.and.ioproc) then
    close(54)
  endif
!
!  CML output
!
  if (lcmlloc) call gulp_cml_endPhonons
  if (lcml.and.lpdfloc) then
    call gulp_cml_PDFstats
    call gulp_cml_outPDF
  endif
!
!  Free local memory
!
  if (lthermal) then
    deallocate(Sij,stat=status)
    if (status/=0) call deallocate_error('phonon','Sij')
  endif
  if (leigloc) then
    deallocate(ramstrength,stat=status)
    if (status/=0) call deallocate_error('phonon','ramstrength')
    deallocate(oscstrength,stat=status)
    if (status/=0) call deallocate_error('phonon','oscstrength')
    deallocate(savefreq,stat=status)
    if (status/=0) call deallocate_error('phonon','savefreq')
  endif
  if (allocated(kpvt)) then
    deallocate(kpvt,stat=status)
    if (status/=0) call deallocate_error('phonon','kpvt')
  endif
  deallocate(mtvrptr,stat=status)
  if (status/=0) call deallocate_error('phonon','mtvrptr')
  deallocate(mtvptr,stat=status)
  if (status/=0) call deallocate_error('phonon','mtvptr')
  if (leckart) then
    deallocate(mcvptr,stat=status)
    if (status/=0) call deallocate_error('phonon','mcvptr')
  endif
  deallocate(meanKEperatom,stat=status)
  if (status/=0) call deallocate_error('phonon','meanKEperatom')
!
!  Close PDF related parts of phonon 
!
  call closepdfphonon
  if (lshengBTE) then
!
!  Output shengBTE control file
!
    call outshengBTEcontrol(iout_shengBTE,.false.)
!
!  Output shengBTE second derivative file
!
    call outshengBTEfc2(iout_shengBTE)
    if (lshengBTE3rd) then
!
!  Output shengBTE third derivative file
!
      t1i = g_cpu_time()
      if (lnoanald3loc) then
        call thirdordern
      else
        call thirdorderfc3
      endif
      t2i = g_cpu_time()
      tfc3 = t2i - t1i + tfc3
!
!  Subtract time from phonon contribution
!
      tphon = tphon - t2i + t1i
    endif
  endif
!
!  Timing
!
  t2t = g_cpu_time()
  tphon = t2t - t1t + tphon
#ifdef TRACE
  call trace_out('phonon')
#endif
!
  return
  end
