  subroutine dynamic_fc
!
!  Master routine for generating second derivatives for phonons
!  when storing as separate cell images for later phasing.
!  On diagonal block elements (i,i) for element i can no longer be
!  generated by summing off diagonals. Therefore these elements
!  are retained from the normal second derivative matrix and
!  added in afterwards in phonon.
!
!  10/14 Created from dynamic
!   2/15 Criterion for calling getBOcharge changed by adding nboQ0
!   4/17 Modified to handle distributed memory second derivatives
!   2/18 Trace added
!   5/18 Modifications for EEM split bond charge added
!   7/18 emat allocated and passed to dcharge routines if needed
!   7/18 Right-hand dimension of emat corrected for case where dcharge is called
!  11/18 Initialisation of density arrays for manybody potentials added
!   8/19 Corrections to call of density3 to include all arguments
!  12/20 density3 now called for EAM as well MEAM
!
!  Conditions of use:
!
!  GULP is available free of charge to academic institutions
!  and non-commerical establishments only. Copies should be
!  obtained from the author only and should not be distributed
!  in any form by the user to a third party without the express
!  permission of the author. This notice applies to all parts
!  of the program, except any library routines which are
!  distributed with the code for completeness. All rights for
!  such routines remain with the original distributor.
!
!  No claim is made that this program is free from errors and
!  no liability will be accepted for any loss or damage that
!  may result. The user is responsible for checking the validity
!  of their results.
!
!  Copyright Curtin University 2020
!
!  Julian Gale, CIC, Curtin University, December 2020
!
  use bondorderdata, only : nbopot, nboQ, nboQ0
  use control
  use current
  use derivatives
  use eam,           only : lMEAM, maxmeamcomponent
  use eembonds,      only : neembond
  use four
  use parallel
  use six
  use sutton
  use m_three
#ifdef TRACE
  use trace,         only : trace_in, trace_out
#endif
  implicit none
!
!  Local variables
!
  integer(i4)                                  :: i
  integer(i4)                                  :: maxlim
  integer(i4)                                  :: maxlimloc
  integer(i4)                                  :: mint
  integer(i4)                                  :: mintloc
  integer(i4)                                  :: nmax
  integer(i4)                                  :: nmaxu
  integer(i4)                                  :: status
  real(dp)                                     :: eatom
  real(dp)                                     :: etmp1
  real(dp)                                     :: etmp2
  real(dp)                                     :: etmp3
  real(dp),    dimension(:,:), allocatable     :: emat
  real(dp),    dimension(:,:), allocatable     :: ematb
#ifdef TRACE
  call trace_in('dynamic_fc')
#endif
!************************************
!  Initialise density for MEAM/EAM  *
!************************************
  if (lsuttonc) then
    if (lMEAM) then
      do i = 1,numat
        scrho(1:maxmeamcomponent,i) = 0.0_dp
        scrho12(1:maxmeamcomponent,i) = 0.0_dp
      enddo
    else
      do i = 1,numat
        scrho(1,i) = 0.0_dp
        scrho12(1,i) = 0.0_dp
      enddo
    endif
  endif
!**********************************************
!  EEM/QEq calculation of charge derivatives  *
!**********************************************
  if (leem) then
!
!  Allocate arrays for matrix equations
!
    nmaxu = numat + 1
    nmax = numat + 1
!
    allocate(emat(nmax,nmaxu),stat=status)
    if (status/=0) call outofmemory('dynamic_fc','emat')
!
    if (leembond) then
      allocate(ematb(nmax,nmaxu),stat=status)
      if (status/=0) call outofmemory('dynamic_fc','ematb')
!
      call dchargesplit(.false.,emat,nmax,ematb,neembond,.true.,.false.)
!
      deallocate(ematb,stat=status)
      if (status/=0) call deallocate_error('dynamic_fc','ematb')
    else
      call dcharge(.false.,emat,nmax,.true.,.false.)
    endif
!
    deallocate(emat,stat=status)
    if (status/=0) call deallocate_error('dynamic_fc','emat')
  endif
!*************************************************
!  Bond Order calculation of charge derivatives  *
!*************************************************
  if ((nboQ+nboQ0).gt.0) then
    call getBOcharge(.true.,.true.)
  endif
!****************************
!  Zero second derivatives  *
!****************************
  mint = 3*numat
  mintloc = 3*natomsonnode
  maxlim = mint
  maxlimloc = mintloc
  if (nbsmat.gt.0) then 
    maxlim = maxlim + numat
    maxlimloc = maxlimloc + natomsonnode
  endif
  if (maxlimloc.gt.maxd2u) then
    maxd2u = maxlimloc
    call changemaxd2
  endif
  if (maxlim.gt.maxd2) then
    maxd2 = maxlim
    call changemaxd2
  endif
!
  derv2(1:maxlim,1:maxlimloc) = 0.0_dp
  d2cell(1:maxlim,1:maxlimloc,1:nd2cells) = 0.0_dp
!*************************
!  Real space component  *
!*************************
  if (nprocs.gt.1) then
    call realfcd
  else
    call realfc
  endif
!**********************************
!  Bond order charge self-energy  *
!**********************************
  if (nboQ0.gt.0) then
    call BOselffc
  endif
!*************************
!  Three-body component  *
!*************************
  if (nthb.gt.0) then
    if (nprocs.gt.1) then
      call threefcd
    else
      call threefc
    endif
  endif
!************************
!  Four-body component  *
!************************
  if (nfor.gt.0) then
    if (nprocs.gt.1) then
      call fourfcd
    else
      call fourfc
    endif
  endif
!***********************
!  Six-body component  *
!***********************
  if (nsix.gt.0) then
    if (nprocs.gt.1) then
      call sixfcd
    else
      call sixfc
    endif
  endif
!************************
!  Many-body component  *
!************************
  if (lsuttonc) then
    eatom = 0.0_dp
    etmp1 = 0.0_dp
    etmp2 = 0.0_dp
    etmp3 = 0.0_dp
    call density3(eatom,etmp1,etmp2,etmp3)
!
    if (nprocs.gt.1) then
      call manyfcd
    else
      call manyfc
    endif
  endif
!**********************
!  Brenner potential  *
!**********************
  if (lbrenner) then
    if (nprocs.gt.1) then
      call brennerfcd
    else
      call brennerfc
    endif
  endif
!************************
!  Bondorder potential  *
!************************
  if (nbopot.gt.0) then
    if (nprocs.gt.1) then
      call bondorderfcd
    else
      call bondorderfc
    endif
  endif
#ifdef TRACE
  call trace_out('dynamic_fc')
#endif
!
  return
  end
